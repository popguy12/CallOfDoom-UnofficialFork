class COD_S1100CB : CODWeapon
{
 	Default
	{
		Weapon.AmmoType "Clip";
		Weapon.AmmoUse 0;
		Weapon.AmmoGive 20;
		Weapon.AmmoType2 "COD_S1100_Mag";
		Weapon.AmmoUse2 0;
		Weapon.AmmoGive2 0;
		
		Weapon.SlotNumber 2;
		
		Obituary "$OB_MPPISTOL";
		Inventory.Pickupmessage "[2] S1-100 CB";
		Tag "S1-100 CB";
		Scale 0.16;
		Inventory.PickupSound "weapons/pistol/pickup";
		+WEAPON.NOALERT;
		+WEAPON.AMMO_OPTIONAL;
		-WEAPON.NOAUTOFIRE;
	}
	
	action void FireWeapon()
	{
		COD_QuakeCamera(2, 2);
		A_AlertMonsters();
		A_Overlay(-3, "MuzzleSmall");
		A_OverlayFlags(-3, PSPF_RENDERSTYLE, true);
		A_OverlayRenderstyle(-3, STYLE_ADD);
		//this handles placing the flash correctly
		A_StartSound("s1100/fire", 0, CHANF_OVERLAP, 1);
		if(CountInv("AimingToken"))
		{
			A_OverlayOffset(-3, 0 - 18, 32 - 54);
		}
		else
		{
			A_OverlayOffset(-3, 0 + 24, 32 - 42);
		}
		//gunsmoke
		A_TakeInventory("COD_S1100_Mag", 1);
		
		COD_FireBullets("COD_762x25mm", 1, frandom(-0.1, 0.1), 0, 0, frandom(-0.1, 0.1));
	}
	
	States
	{
	
	Deselect:
		TNT1 A 0 A_StartSound("pm/lower", 0, CHANF_OVERLAP, 1);
		M3GA FGEDCBA 1;
		PSTG A 0 A_Lower(25);
		Wait;
	User4:
		M3GA FGEDCBA 1;
		Goto NVToggle;
	Select:
		TNT1 A 0
		{
			A_TakeInventory("AimingToken");
		}
		TNT1 A 0 A_Raise(25);
		Wait;
	Ready:
		TNT1 A 0 A_StartSound("pm/raise", 0, CHANF_OVERLAP, 1);
		M3GA ABCDEGF 1;
	WeaponReady:
		M3GA F 1 COD_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		Loop;
	WeaponReady2:
		M3GC D 1 COD_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		Loop;
	Fire:
		TNT1 A 0 A_JumpIfInventory("AimingToken", 1, "Fire2");
		TNT1 A 0 A_JumpIf(CountInv("COD_S1100_Mag") == 0, "DryFire");
		M3GB A 1 FireWeapon();
		M3GB BCDE 1;
		Goto WeaponReady;
	Fire2:
		TNT1 A 0 A_JumpIf(CountInv("COD_S1100_Mag") == 0, "DryFire2");
		M3GD A 1 FireWeapon();
		M3GD BCDE 1;
		Goto WeaponReady2;
	AltFire:
		TNT1 A 0 A_JumpIfInventory("AimingToken", 1, "AltFire2");
		TNT1 A 0 A_GiveInventory("AimingToken");
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP);
		M3GC ABCEFGD 1;
		Goto WeaponReady2;
	AltFire2:
		TNT1 A 0 A_TakeInventory("AimingToken");
		TNT1 A 0 A_StartSound("uni/clothfoleys", 0, CHANF_OVERLAP);
		M3GC DGFECBA 1;
		Goto WeaponReady;
	
	DryFire:
		M3GB D 1;
		Goto WeaponReady;
	DryFire2:
		M3GD E 1;
		Goto WeaponReady2;
	
	UnAimReload:
		TNT1 A 0 A_TakeInventory("AimingToken");
		M3GC DGFECBA 1;
	Reload:
		TNT1 A 0 A_JumpIf(CountInv("AimingToken") == 1, "UnAimReload");
		TNT1 A 0 A_JumpIf(CountInv("COD_S1100_Mag") == 0 && CountInv("Clip") == 0, "WeaponReadyEmpty");
		TNT1 A 0 A_JumpIf(CountInv("COD_S1100_Mag") == 32 || CountInv("Clip") == 0, "WeaponReady");
		TNT1 A 0
		{
			if(CountInv("COD_S1100_Mag") == 0)
			{
				A_GiveInventory("ChamberMeToken", 1);
			}
		}
		TNT1 A 0 A_TakeInventory("AimingToken");
		TNT1 A 0 A_StartSound("uni/clothfoleym");
		M3GE ABCDEFGHIJKL 1;
		TNT1 A 0 A_StartSound("s1100/out", 0, CHANF_OVERLAP);
		M3GE MNOPQ 1;
		TNT1 A 0 A_StartSound("uni/magfol", 0, CHANF_OVERLAP);
		M3GE RSTUVWXYZ 1;
		TNT1 A 0 A_StartSound("s1100/in", 0, CHANF_OVERLAP);
		M3GF ABCDEFGH 1;
		TNT1 A 0 A_JumpIfInventory("ChamberMeToken", 1, "Reload2");
		TNT1 A 0 COD_AmmoIntoMag("COD_S1100_Mag", "Clip", 32, 1);
		M3GF IJKLMNOPQRST 1;
		Goto WeaponReady;
	Reload2:
		TNT1 A 0 A_TakeInventory("AimingToken");
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP);
		M3GG ABCDEF 1;
		TNT1 A 0 A_StartSound("s1100/rack", 0, CHANF_OVERLAP);
		TNT1 A 0 COD_AmmoIntoMag("COD_S1100_Mag", "Clip", 32 , 1);
		TNT1 A 0 A_TakeInventory("ChamberMeToken", 1);
		M3GG GHIJKLM 1;
		M3GG NO 1;
		TNT1 A 0 A_StartSound("uni/clothfoleyl", 0, CHANF_OVERLAP);
		M3GG PQRSTUVWX 1;
		Goto WeaponReady;
 	Spawn:
		B003 S -1;
		Stop;
	
	}
}

Class COD_S1100_Mag : Ammo
{
	Default
	{
		Inventory.Amount 0;
		Inventory.MaxAmount 32;
		Ammo.BackpackAmount 0;
		Ammo.BackpackMaxAmount 32;
	}
}
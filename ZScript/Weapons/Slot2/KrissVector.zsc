class COD_Vector : CODWeapon
{
 	Default
	{
		Weapon.AmmoType "Clip";
		Weapon.AmmoUse 0;
		Weapon.AmmoGive 20;
		Weapon.AmmoType2 "COD_Vector_Mag";
		Weapon.AmmoUse2 0;
		Weapon.AmmoGive2 0;
		
		Weapon.SlotNumber 2;
		
		/*
		zoom 1.0
		zoomaim 1.2
		zoomdelta 0.01
		zoom tics 1
		*/
		
		Obituary "$OB_MPPISTOL";
		Inventory.Pickupmessage "[2] Kriss Vector .45 ACP";
		Tag "Kriss Vector";
		Scale 0.16;
		Inventory.PickupSound "weapons/pistol/pickup";
		+WEAPON.NOALERT;
		+WEAPON.AMMO_OPTIONAL;
		-WEAPON.NOAUTOFIRE;
		
		CODWeapon.PlaySpeed 0.85;
		CODWeapon.SlotType 1;
		CODWeapon.InfoGraphics "ISVECTOR", "ICVECN", "ICVECU", "WLVECTOR";
	}
	
	action void FireWeapon()
	{
		//A_AlertMonsters();
		//A_Overlay(-3, "MuzzleSmall");
		//A_OverlayFlags(-3, PSPF_ALPHA, true);
		//A_OverlayFlags(-3, PSPF_RENDERSTYLE, true);
		//A_OverlayRenderstyle(-3, STYLE_ADD);
		
		A_StartSound("kriss/fire", 0, CHANF_OVERLAP, 1);
		A_StartSound("kriss/fireadd", 0, CHANF_OVERLAP, 0.75);
		COD_FireBullets("COD_45ACP", 1, frandom(-0.1, 0.1), 0, 0, frandom(-0.1, 0.1));
		//A_SpawnItemEx("PlayerMuzzleFlash",30,0,45);
		
		//this handles placing the flash correctly
		if(CountInv("AimingToken"))
		{
			//A_OverlayOffset(-3, 0 - 18, 32 - 54);
			COD_HandleWeaponFeedback(2, 2, -0.40, frandom(+0.30, -0.30), 0, 0, 0);
			A_ZoomFactor(1.2-0.01);
		}
		else
		{
			//A_OverlayOffset(-3, 0 + 20, 32 - 42);
			COD_HandleWeaponFeedback(2, 2, -0.80, frandom(+0.60, -0.60), -4, 0, 0);
			A_ZoomFactor(1-0.01);
		}
		//gunsmoke
		A_TakeInventory("COD_Vector_Mag", 1);
	}
	
	States
	{
	
	Deselect:
		TNT1 A 0 A_StartSound("kriss/lower", 0, CHANF_OVERLAP, 1);
		TNT1 A 0 A_ZoomFactor(1);
		TNT1 A 0 A_ClearOverlays(-2,-2);
		VECA FJIHGEDCBA 1;
		PSTG A 0 A_Lower(25);
		Wait;
	User4:
		TNT1 A 0 A_ClearOverlays(-2,-2);
		VECA FJIHGED 1;
		TNT1 A 0 A_ZoomFactor(1);
		TNT1 A 0 A_TakeInventory("AimingToken");
		VECA CBA 1;
		Goto NVToggle;
	User2:
		TNT1 A 0 A_ClearOverlays(-2,-2);
		VECA FJIHGED 1;
		TNT1 A 0 A_ZoomFactor(1);
		TNT1 A 0 A_TakeInventory("AimingToken");
		VECA CBA 1;
		Goto RadToggle;
	User3:
		TNT1 A 0 A_ClearOverlays(-2,-2);
		TNT1 A 0 A_ZoomFactor(1);
		TNT1 A 0 A_TakeInventory("AimingToken");
		Goto KnifeAttack;
	Select:
		TNT1 A 0
		{
			A_TakeInventory("AimingToken");
			A_TakeInventory("DoAttachment");
		}
		TNT1 A 0 A_Raise(25);
		Wait;
	Ready:
		TNT1 A 0 A_StartSound("kriss/raise", 0, CHANF_OVERLAP, 1);
		TNT1 A 6;
		VECA ABCDEGHIJF 1;
	WeaponReady:
		VECA F 1 COD_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER2 | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		TNT1 A 0 A_JumpIfInventory("ThrowGrenade", 1, "FragGrenade");
		TNT1 A 0 A_JumpIfInventory("ThrowBang", 1, "StunGrenade");
		Loop;
	WeaponReady2:
		VECC D 1 COD_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER2 | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		TNT1 A 0 A_JumpIfInventory("ThrowGrenade", 1, "FragGrenade");
		TNT1 A 0 A_JumpIfInventory("ThrowBang", 1, "StunGrenade");
		Loop;
	Fire:
		TNT1 A 0 A_JumpIfInventory("AimingToken", 1, "Fire2");
		TNT1 A 0 A_JumpIf(CountInv("COD_Vector_Mag") == 0, "DryFire");
		VECB A 1 FireWeapon();
		TNT1 A 0 A_ZoomFactor(1);
		VECB B 1;
		VECB CD 1 A_ReFire("Fire");
		Goto WeaponReady;
	Fire2:
		TNT1 A 0 A_JumpIf(CountInv("COD_Vector_Mag") == 0, "DryFire2");
		VECD A 1 FireWeapon();
		TNT1 A 0 A_ZoomFactor(1.2);
		VECD B 1;
		VECD CD 1 A_ReFire("Fire2");
		Goto WeaponReady2;
	AltFire:
		TNT1 A 0 A_JumpIfInventory("AimingToken", 1, "AltFire2");
		TNT1 A 0 A_GiveInventory("AimingToken");
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP);
		VECC AB 1;
		TNT1 A 0 A_ZoomFactor(1.2);
		TNT1 A 0 A_Overlay(-2, "Reticle");
		VECC CEF 1;
		Goto WeaponReady2;
	AltFire2:
		TNT1 A 0 A_TakeInventory("AimingToken");
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP);
		VECC FE 1;
		TNT1 A 0 A_ClearOverlays(-2,-2);
		TNT1 A 0 A_ZoomFactor(1);
		VECC CBA 1;
		Goto WeaponReady;
	
	DryFire:
		VECB D 1;
		Goto WeaponReady;
	DryFire2:
		VECD D 1;
		Goto WeaponReady2;
	
	UnAimReload:
		TNT1 A 0 A_TakeInventory("AimingToken");
		VECC FE 1;
		TNT1 A 0 A_ClearOverlays(-2,-2);
		TNT1 A 0 A_ZoomFactor(1);
		VECC CBA 1;
	Reload:
		TNT1 A 0 A_JumpIf(CountInv("AimingToken") == 1, "UnAimReload");
		TNT1 A 0 A_JumpIf(CountInv("COD_Vector_Mag") == 0 && CountInv("Clip") == 0, "WeaponReadyEmpty");
		TNT1 A 0 A_JumpIf(CountInv("COD_Vector_Mag") == 41 || CountInv("Clip") == 0, "WeaponReady");
		TNT1 A 0
		{
			if(CountInv("COD_Vector_Mag") == 0)
			{
				A_GiveInventory("ChamberMeToken", 1);
			}
		}
		TNT1 A 0 A_TakeInventory("AimingToken");
		TNT1 A 0 A_StartSound("uni/clothfoleym");
		VECE ABCDEFGHI 1;
		TNT1 A 0 A_StartSound("kriss/bolt", 0, CHANF_OVERLAP, 0.25);
		VECE JKL 2;
		TNT1 A 0 A_StartSound("kriss/magout", 0, CHANF_OVERLAP, 1);
		VECE MNOPQRSTU 1;
		TNT1 A 0 A_StartSound("uni/clothfoleys", 0, CHANF_OVERLAP, 1);
		VECE VW 1;
		VECE XYZ 2;
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP, 1);
		VECF ABC 2;
		TNT1 A 0 A_StartSound("uni/magfoley", 0, CHANF_OVERLAP, 1);
		VECF DEFGHIJKLM 1;
		TNT1 A 0 A_StartSound("kriss/magin", 0, CHANF_OVERLAP, 1);
		VECF NOP 2;
		TNT1 A 0 A_JumpIfInventory("ChamberMeToken", 1, "Reload2");
		TNT1 A 0 COD_AmmoIntoMag("COD_Vector_Mag", "Clip", 41, 1);
		VECF QRST 1;
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP, 1);
		VECF UV 2;
		VECF WX 1;
		Goto WeaponReady;
	Reload2:
		TNT1 A 0 A_TakeInventory("AimingToken");
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP);
		VECF QRST 1;
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP, 1);
		VECG ABCDEF 1;
		TNT1 A 0 A_StartSound("kriss/bolt", 0, CHANF_OVERLAP);
		TNT1 A 0 COD_AmmoIntoMag("COD_Vector_Mag", "Clip", 40 , 1);
		TNT1 A 0 A_TakeInventory("ChamberMeToken", 1);
		VECG G 1;
		VECG HIJ 2;
		VECG K 1;
		TNT1 A 0 A_StartSound("uni/clothfoleys", 0, CHANF_OVERLAP, 1);
		VECG LMNOPQ 1;
		Goto WeaponReady;
	
	Reticle:
		TNT1 A 0;
		TNT1 A 0
		{
			A_OverlayFlags(-2, PSPF_ADDBOB, false);
		}
		VECC X 35;
		Loop;
 	Spawn:
		B002 B -1;
		Stop;
	
	}
}

Class COD_Vector_Mag : Ammo
{
	Default
	{
		Inventory.Amount 0;
		Inventory.MaxAmount 41;
		Ammo.BackpackAmount 0;
		Ammo.BackpackMaxAmount 41;
	}
}
class COD_M14EBR : CODWeapon
{
 	Default
	{
		Weapon.AmmoType "Clip";
		Weapon.AmmoUse 0;
		Weapon.AmmoGive 5;
		Weapon.AmmoType2 "COD_M14EBR_Mag";
		Weapon.AmmoUse2 0;
		Weapon.AmmoGive2 0;
		
		Weapon.SlotNumber 7;
		
		/*
		zoom 1.0
		zoomaim 1.2
		zoomdelta 0.2
		zoom tics 1
		*/
		
		Obituary "$OB_MPPISTOL";
		Inventory.Pickupmessage "[7] M14 EBR";
		Tag "M14 EBR";
		Scale 0.16;
		Inventory.PickupSound "weapons/pistol/pickup";
		+WEAPON.NOALERT;
		+WEAPON.AMMO_OPTIONAL;
		
		CODWeapon.PlaySpeed 0.75;
		CODWeapon.SlotType 1;
		CODWeapon.InfoGraphics "ISM1AEBR", "ICMK14N", "ICMK14U", "WLMK14";
	}
	
	action void FireWeapon()
	{
		A_AlertMonsters();
		A_Overlay(-3, "MuzzleMedium");
		A_OverlayFlags(-3, PSPF_ALPHA, true);
		A_OverlayFlags(-3, PSPF_RENDERSTYLE, true);
		A_OverlayRenderstyle(-3, STYLE_ADD);
		
		A_StartSound("M14/fire", 0, CHANF_OVERLAP, 1);
		A_StartSound("M14/fireadd", 0, CHANF_OVERLAP, 0.8);
		COD_FireBullets("COD_762x51mm", 1, frandom(-0.1, 0.1), 0, 0, frandom(-0.1, 0.1));
		A_SpawnItemEx("PlayerMuzzleFlash",30,0,45);
		
		//this handles placing the flash correctly
		if(CountInv("AimingToken"))
		{
			A_OverlayOffset(-3, 0 - 18, 32 - 54);
			COD_HandleWeaponFeedback(3, 3, -0.70, frandom(+0.50, -0.50), 0, 0, 0);
			A_ZoomFactor(2.5-0.2);
		}
		else
		{
			A_OverlayOffset(-3, 0 + 12, 32 - 50);
			COD_HandleWeaponFeedback(3, 3, -1.3, frandom(+0.9, -0.9), -3, 0, 0);
			A_ZoomFactor(1-0.2);
		}
		//gunsmoke
		A_TakeInventory("COD_M14EBR_Mag", 1);
	}
	
	States
	{
	Deselect:
		TNT1 A 0 A_StartSound("A88/lower", 0, CHANF_OVERLAP, 1);
		TNT1 A 0 A_ZoomFactor(1);
		M14A FLKJIHGEDCBA 1;
		PSTG A 0 A_Lower(25);
		Wait;
	User4:
		M14A FGED 1;
		TNT1 A 0 A_ZoomFactor(1);
		TNT1 A 0 A_TakeInventory("AimingToken");
		M14A CBA 1;
		Goto NVToggle;
	User2:
		M14A FGED 1;
		TNT1 A 0 A_ZoomFactor(1);
		TNT1 A 0 A_TakeInventory("AimingToken");
		M14A CBA 1;
		Goto RadToggle;
	User3:
		TNT1 A 0 A_ZoomFactor(1);
		TNT1 A 0 A_TakeInventory("AimingToken");
		Goto KnifeAttack;
	Select:
		TNT1 A 0
		{
			A_TakeInventory("AimingToken");
			A_TakeInventory("DoAttachment");
		}
		TNT1 A 0 A_Raise(25);
		Wait;
	Ready:
		TNT1 A 0 A_StartSound("A88/raise", 0, CHANF_OVERLAP, 1);
		TNT1 A 12;
		M14A ABCDEGHIJKLF 1;
	WeaponReady:
		M14A F 1 COD_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER2 | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		TNT1 A 0 A_JumpIf(CountInv("COD_M14EBR_Mag") == 0, "WeaponReadyEmpty");
		TNT1 A 0 A_JumpIfInventory("ThrowGrenade", 1, "FragGrenade");
		TNT1 A 0 A_JumpIfInventory("ThrowBang", 1, "StunGrenade");
		Loop;
	WeaponReady2:
		M14C D 1 COD_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER2 | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		TNT1 A 0 A_JumpIf(CountInv("COD_M14EBR_Mag") == 0, "WeaponReady2Empty");
		TNT1 A 0 A_JumpIfInventory("ThrowGrenade", 1, "FragGrenade");
		TNT1 A 0 A_JumpIfInventory("ThrowBang", 1, "StunGrenade");
		Loop;
	WeaponReadyEmpty:
		M14B A 1 COD_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER2 | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		TNT1 A 0 A_JumpIfInventory("ThrowGrenade", 1, "FragGrenade");
		TNT1 A 0 A_JumpIfInventory("ThrowBang", 1, "StunGrenade");
		Loop;
	WeaponReady2Empty:
		M14D C 1 COD_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER2 | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		TNT1 A 0 A_JumpIfInventory("ThrowGrenade", 1, "FragGrenade");
		TNT1 A 0 A_JumpIfInventory("ThrowBang", 1, "StunGrenade");
		Loop;
	Fire:
		TNT1 A 0 A_JumpIfInventory("AimingToken", 1, "Fire2");
		TNT1 A 0 A_JumpIf(CountInv("COD_M14EBR_Mag") == 0, "DryFire");
		M14B A 1 FireWeapon();
		TNT1 A 0 A_ZoomFactor(1);
		M14B BC 1;
		TNT1 A 0 A_JumpIf(CountInv("COD_M14EBR_Mag") == 0, "WeaponReadyEmpty");
		M14B ED 1;
		Goto WeaponReady;
	Fire2:
		TNT1 A 0 A_JumpIf(CountInv("COD_M14EBR_Mag") == 0, "DryFire2");
		M14D A 1 FireWeapon();
		TNT1 A 0 A_ZoomFactor(2.5);
		M14D BC 1;
		TNT1 A 0 A_JumpIf(CountInv("COD_M14EBR_Mag") == 0, "WeaponReady2Empty");
		M14D DE 1;
		Goto WeaponReady2;
	AltFire:
		TNT1 A 0 A_JumpIfInventory("AimingToken", 1, "AltFire2");
		TNT1 A 0 A_GiveInventory("AimingToken");
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP, 1);
		M14C ABC 1;
		TNT1 A 0 A_ZoomFactor(2.5);
		M14C EFD 1;
		Goto WeaponReady2;
	AltFire2:
		TNT1 A 0 A_TakeInventory("AimingToken");
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP, 1);
		M14C DFE 1;
		TNT1 A 0 A_ZoomFactor(1);
		M14C CBA 1;
		Goto WeaponReady;
	
	DryFire:
		M14B A 1;
		Goto WeaponReadyEmpty;
	DryFire2:
		M14D C 1;
		Goto WeaponReady2Empty;
	
	UnAimReload:
		TNT1 A 0 A_TakeInventory("AimingToken");
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP, 1);
		M14C DFE 1;
		TNT1 A 0 A_ZoomFactor(1);
		M14C CBA 1;
	Reload:
		TNT1 A 0 A_JumpIf(CountInv("AimingToken") == 1, "UnAimReload");
		TNT1 A 0 A_JumpIf(CountInv("COD_M14EBR_Mag") == 0 && CountInv("Clip") == 0, "WeaponReadyEmpty");
		TNT1 A 0 A_JumpIf(CountInv("COD_M14EBR_Mag") == 21 || CountInv("Clip") == 0, "WeaponReady");
		TNT1 A 0 A_JumpIf(CountInv("COD_M14EBR_Mag") == 0, "ReloadEmpty");
		TNT1 A 0 A_TakeInventory("AimingToken");
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP);
		M14E ABCDEFGHI 1;
		TNT1 A 0 A_StartSound("M14/MagOut", 0, CHANF_OVERLAP);
		M14E JKLMN 2;
		M14E OPQR 1;
		TNT1 A 0 A_StartSound("uni/clothfoleys", 0, CHANF_OVERLAP);
		M14E STUV 2;
		TNT1 A 0 A_StartSound("uni/magfol", 0, CHANF_OVERLAP);
		M14E WXZ 2;
		M14F ABCDE 2;
		TNT1 A 0 A_StartSound("M14/magin", 0, CHANF_OVERLAP);
		M14F FG 1;
		TNT1 A 0 COD_AmmoIntoMag("COD_M14EBR_Mag", "Clip", 21, 1);
		M14F HIJ 2;
		M14F KL 1;
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP);
		M14F MNO 1;
		Goto WeaponReady;
	ReloadEmpty:
		TNT1 A 0 A_TakeInventory("AimingToken");
		TNT1 A 0 A_StartSound("uni/clothfoleys", 0, CHANF_OVERLAP);
		M14G ABCDEFGH 1;
		TNT1 A 0 A_StartSound("M14/magout", 0, CHANF_OVERLAP);
		M14G IJ 1;
		TNT1 A 0 A_StartSound("uni/clothfoleys", 0, CHANF_OVERLAP);
		M14G KLMNO 1;
		M14G PQRS 2;
		TNT1 A 0 A_StartSound("uni/magfol", 0, CHANF_OVERLAP);
		M14G TUVWXYZ 2;
		TNT1 A 0 A_StartSound("M14/magin", 0, CHANF_OVERLAP);
		M14H ABCD 2;
		TNT1 A 0 COD_AmmoIntoMag("COD_M14EBR_Mag", "Clip", 20 , 1);
		TNT1 A 0 A_StartSound("uni/clothfoleys", 0, CHANF_OVERLAP);
		M14H EFGH 2;
		TNT1 A 0 A_StartSound("M14/charge", 0, CHANF_OVERLAP);
		M14H IJKL 1;
		M14H M 3;
		M14H NOPQR 2;
		M14H STUVWX 1;
		Goto WeaponReady;
 	Spawn:
		B002 E -1;
		Stop;
	
	}
}

Class COD_M14EBR_Mag : Ammo
{
	Default
	{
		Inventory.Amount 0;
		Inventory.MaxAmount 21;
		Ammo.BackpackAmount 0;
		Ammo.BackpackMaxAmount 21;
	}
}
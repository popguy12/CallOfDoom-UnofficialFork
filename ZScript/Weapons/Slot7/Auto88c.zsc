class COD_Auto88 : CODWeapon
{
 	Default
	{
		Weapon.AmmoType "Clip";
		Weapon.AmmoUse 0;
		Weapon.AmmoGive 5;
		Weapon.AmmoType2 "COD_Auto88_Mag";
		Weapon.AmmoUse2 0;
		Weapon.AmmoGive2 0;
		
		Weapon.SlotNumber 7;
		
		/*
		zoom 1.0
		zoomaim 1.2
		zoomdelta 0.2
		zoom tics 1
		*/
		
		Obituary "$OB_MPPISTOL";
		Inventory.Pickupmessage "[7] Auto-88c";
		Tag "Auto-88c";
		Scale 0.16;
		Inventory.PickupSound "weapons/pistol/pickup";
		+WEAPON.NOALERT;
		+WEAPON.AMMO_OPTIONAL;
	}
	
	action void FireWeapon()
	{
		A_AlertMonsters();
		A_Overlay(-3, "MuzzleMedium");
		A_OverlayFlags(-3, PSPF_ALPHA, true);
		A_OverlayFlags(-3, PSPF_RENDERSTYLE, true);
		A_OverlayRenderstyle(-3, STYLE_ADD);
		
		A_StartSound("a88/fire", 0, CHANF_OVERLAP, 1);
		COD_FireBullets("COD_762x51mm", 1, frandom(-0.1, 0.1), 0, 0, frandom(-0.1, 0.1));
		A_SpawnItemEx("PlayerMuzzleFlash",30,0,45);
		
		//this handles placing the flash correctly
		if(CountInv("AimingToken"))
		{
			A_OverlayOffset(-3, 0 - 18, 32 - 54);
			COD_HandleWeaponFeedback(3, 3, -0.70, frandom(+0.50, -0.50), 0, 0, 0);
			A_ZoomFactor(1.2-0.2);
		}
		else
		{
			A_OverlayOffset(-3, 0 + 12, 32 - 50);
			COD_HandleWeaponFeedback(3, 3, -1.3, frandom(+0.9, -0.9), -3, 0, 0);
			A_ZoomFactor(1-0.2);
		}
		//gunsmoke
		A_TakeInventory("COD_Auto88_Mag", 1);
	}
	
	States
	{
	Deselect:
		TNT1 A 0 A_StartSound("A88/lower", 0, CHANF_OVERLAP, 1);
		TNT1 A 0 A_ZoomFactor(1);
		TAPA FGEDCBA 1;
		PSTG A 0 A_Lower(25);
		Wait;
	User4:
		TAPA FGED 1;
		TNT1 A 0 A_ZoomFactor(1);
		TNT1 A 0 A_TakeInventory("AimingToken");
		TAPA CBA 1;
		Goto NVToggle;
	User2:
		TAPA FGED 1;
		TNT1 A 0 A_ZoomFactor(1);
		TNT1 A 0 A_TakeInventory("AimingToken");
		TAPA CBA 1;
		Goto RadToggle;
	User3:
		TNT1 A 0 A_ZoomFactor(1);
		TNT1 A 0 A_TakeInventory("AimingToken");
		Goto KnifeAttack;
	Select:
		TNT1 A 0
		{
			A_TakeInventory("AimingToken");
		}
		TNT1 A 0 A_Raise(25);
		Wait;
	Ready:
		TNT1 A 0 A_StartSound("A88/raise", 0, CHANF_OVERLAP, 1);
		TNT1 A 12;
		TAPA ABCDEGF 1;
	WeaponReady:
		TAPA F 1 COD_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		TNT1 A 0 A_JumpIf(CountInv("COD_Auto88_Mag") == 0, "WeaponReadyEmpty");
		TNT1 A 0 A_JumpIfInventory("ThrowGrenade", 1, "FragGrenade");
		TNT1 A 0 A_JumpIfInventory("ThrowBang", 1, "StunGrenade");
		Loop;
	WeaponReady2:
		TAPC D 1 COD_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		TNT1 A 0 A_JumpIf(CountInv("COD_Auto88_mag") == 0, "WeaponReady2Empty");
		TNT1 A 0 A_JumpIfInventory("ThrowGrenade", 1, "FragGrenade");
		TNT1 A 0 A_JumpIfInventory("ThrowBang", 1, "StunGrenade");
		Loop;
	WeaponReadyEmpty:
		TAPB C 1 COD_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		TNT1 A 0 A_JumpIfInventory("ThrowGrenade", 1, "FragGrenade");
		TNT1 A 0 A_JumpIfInventory("ThrowBang", 1, "StunGrenade");
		Loop;
	WeaponReady2Empty:
		TAPD C 1 COD_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		TNT1 A 0 A_JumpIfInventory("ThrowGrenade", 1, "FragGrenade");
		TNT1 A 0 A_JumpIfInventory("ThrowBang", 1, "StunGrenade");
		Loop;
	Fire:
		TNT1 A 0 A_JumpIfInventory("AimingToken", 1, "Fire2");
		TNT1 A 0 A_JumpIf(CountInv("COD_Auto88_mag") == 0, "DryFire");
		TAPB A 1 FireWeapon();
		TNT1 A 0 A_ZoomFactor(1);
		TAPB BC 1;
		TNT1 A 0 A_JumpIf(CountInv("COD_Auto88_mag") == 0, "WeaponReadyEmpty");
		TAPB ED 1;
		Goto WeaponReady;
	Fire2:
		TNT1 A 0 A_JumpIf(CountInv("COD_Auto88_mag") == 0, "DryFire2");
		TAPD A 1 FireWeapon();
		TNT1 A 0 A_ZoomFactor(1.2);
		TAPD BC 1;
		TNT1 A 0 A_JumpIf(CountInv("COD_Auto88_mag") == 0, "WeaponReady2Empty");
		TAPD DE 1;
		Goto WeaponReady2;
	AltFire:
		TNT1 A 0 A_JumpIfInventory("AimingToken", 1, "AltFire2");
		TNT1 A 0 A_GiveInventory("AimingToken");
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP, 1);
		TAPC ABC 1;
		TNT1 A 0 A_ZoomFactor(1.2);
		TAPC EFD 1;
		Goto WeaponReady2;
	AltFire2:
		TNT1 A 0 A_TakeInventory("AimingToken");
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP, 1);
		TAPC DFE 1;
		TNT1 A 0 A_ZoomFactor(1);
		TAPC CBA 1;
		Goto WeaponReady;
	
	DryFire:
		TAPB C 1;
		Goto WeaponReadyEmpty;
	DryFire2:
		TAPD C 1;
		Goto WeaponReady2Empty;
	
	UnAimReload:
		TNT1 A 0 A_TakeInventory("AimingToken");
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP, 1);
		TAPC DFE 1;
		TNT1 A 0 A_ZoomFactor(1);
		TAPC CBA 1;
	Reload:
		TNT1 A 0 A_JumpIf(CountInv("AimingToken") == 1, "UnAimReload");
		TNT1 A 0 A_JumpIf(CountInv("COD_Auto88_mag") == 0 && CountInv("Clip") == 0, "WeaponReadyEmpty");
		TNT1 A 0 A_JumpIf(CountInv("COD_Auto88_mag") == 5 || CountInv("Clip") == 0, "WeaponReady");
		TNT1 A 0 A_JumpIf(CountInv("COD_Auto88_mag") == 0, "ReloadEmpty");
		TNT1 A 0 A_JumpIf(CountInv("COD_Auto88_mag") <= 2, "ReloadEjectLast");
		TNT1 A 0 A_JumpIf(CountInv("COD_Auto88_mag") >= 3, "ReloadCart");
	ReloadEmpty:
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP, 1);
		TAPF ABCDEF 1;
		TNT1 A 0 A_StartSound("A88/rackback", 0, CHANF_OVERLAP, 1);
		TAPF GHIJKLMOP 1;
	ReloadInsert:
		TAPF QRSTUVWXYZ 1;
		TNT1 A 0 A_StartSound("a88/clipset", 0, CHANF_OVERLAP, 1);
		TAPG ABCDEF 1;
		TNT1 A 0 A_StartSound("uni/clothfoleys", 0, CHANF_OVERLAP, 1);
		TAPG GHIJKL 1;
		TNT1 A 0 A_StartSound("a88/clipin", 0, CHANF_OVERLAP, 1);
		TNT1 A 0 COD_AmmoIntoMag("COD_Auto88_mag", "Clip", 5, 1);
		TAPG MNOPQR 1;
		TNT1 A 0 A_StartSound("uni/clothfoleys", 0, CHANF_OVERLAP, 1);
		TAPG RSTUV 1;
		TNT1 A 0 A_StartSound("sks/cliprem", 0, CHANF_OVERLAP, 1); //[Pop] yes, SKS sound reused here, maybe replace later?
		TAPG WXYZ 1;
	ReloadEnd:
		TNT1 A 0 A_StartSound("uni/clothfoleys", 0, CHANF_OVERLAP, 1);
		TAPI ABCDEFGHIJK 1;
		TNT1 A 0 A_StartSound("A88/rackforward", 0, CHANF_OVERLAP, 1);
		TAPI LMNOPQRS 1;
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP, 1);
		TAPI TUVWXYZ 1;
		Goto WeaponReady;
	ReloadCart:
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP, 1);
		TAPF ABCDEF 1;
		TNT1 A 0 A_StartSound("A88/rackback", 0, CHANF_OVERLAP, 1);
		TAPF GHIJKLMOP 1;
		TAPH ABCDEFG 1;
	ReloadCartLoop:
		TAPH HIJ 1;
		TNT1 A 0 A_StartSound("A88/cart", 0, CHANF_OVERLAP, 1);
		TAPH KLMNOP 1;
		TNT1 A 0 COD_AmmoIntoMag("COD_Auto88_mag", "Clip", CountInv("COD_Auto88_Mag") + 1, 1);
		TAPH QR 1;
		TNT1 A 0 A_JumpIf(CountInv("COD_Auto88_mag") == 5 || CountInv("Clip") == 0, "ReloadEnd");
		Loop;
	ReloadEjectLast:
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP, 1);
		TAPF ABCDEFG 1;
		TNT1 A 0 A_StartSound("A88/rackquick", 0, CHANF_OVERLAP, 1);
		TAPF HIJKLM 1;
		TNT1 A 0 A_JumpIf(CountInv("COD_Auto88_mag") == 1, "ReloadEjectLastSkip");
		TAPF MLKJI 1;
		TNT1 A 0 A_StartSound("A88/rackquick", 0, CHANF_OVERLAP, 1);
		TNT1 A 0 A_StartSound("uni/clothfoleys", 0, CHANF_OVERLAP, 1);
		TAPF HIJKLM 1;
	ReloadEjectLastSkip:
		TAPF NOP 1;
		Goto ReloadInsert;
 	Spawn:
		B002 E -1;
		Stop;
	
	}
}

Class COD_Auto88_Mag : Ammo
{
	Default
	{
		Inventory.Amount 0;
		Inventory.MaxAmount 5;
		Ammo.BackpackAmount 0;
		Ammo.BackpackMaxAmount 5;
	}
}
class COD_Model30 : CODWeapon
{
 	Default
	{
		Weapon.AmmoType "Shell";
		Weapon.AmmoUse 0;
		Weapon.AmmoGive 4;
		Weapon.AmmoType2 "COD_M30_Tube";
		Weapon.AmmoUse2 0;
		Weapon.AmmoGive2 0;
		
		CODWeapon.AmmoType3 "COD_M30_UB";
		CODWeapon.AmmoUse3 0;
		CODWeapon.AmmoGive3 0;
		
		Weapon.SlotNumber 3;
		
		/*
		zoom 1.0
		zoomaim 1.2
		zoomdelta 0.1
		zoom tics 2
		*/
		
		Obituary "$OB_MPPISTOL";
		Inventory.Pickupmessage "[3] Model 30";
		Tag "Model 30";
		Scale 0.16;
		Inventory.PickupSound "weapons/pistol/pickup";
		+WEAPON.NOALERT;
		+WEAPON.AMMO_OPTIONAL;
		
		CODWeapon.PlaySpeed 0.8;
		CODWeapon.SlotType 1;
		CODWeapon.InfoGraphics "ISM30LD", "ICAA12N", "ICAA12U", "WLAA12";
	}
	
	action void FireWeapon()
	{
		A_AlertMonsters();
		A_Overlay(-3, "MuzzleBig");
		A_OverlayFlags(-3, PSPF_ALPHA, true);
		A_OverlayFlags(-3, PSPF_RENDERSTYLE, true);
		A_OverlayRenderstyle(-3, STYLE_ADD);
		
		A_StartSound("m30/fire", 0, CHANF_OVERLAP, 1);
		A_StartSound("m30/fireadd", 0, CHANF_OVERLAP, 0.75);
		COD_FireBullets("COD_12GAPellet", 9, frandom(-6.0, 6.0), 0, 0, frandom(-4.0, 4.0));
		A_SpawnItemEx("PlayerMuzzleFlash",30,0,45);
		
		//this handles placing the flash correctly
		if(CountInv("AimingToken"))
		{
			A_OverlayOffset(-3, 0 - 18, 32 - 54);
			COD_HandleWeaponFeedback(4, 6, -1.3, frandom(+1.0, -1.0), 0, 0, 0);
			A_ZoomFactor(1.2-0.1);
		}
		else
		{
			A_OverlayOffset(-3, 0 + 12, 32 - 50);
			COD_HandleWeaponFeedback(4, 6, -2.60, frandom(+2.0, -2.0), -3, 0, 0);
			A_ZoomFactor(1-0.1);
		}
		//gunsmoke
		A_TakeInventory("COD_M30_Tube", 1);
	}
	
	action void FireWeaponUB()
	{
		A_AlertMonsters();
		A_Overlay(-3, "MuzzleMedium");
		A_OverlayFlags(-3, PSPF_ALPHA, true);
		A_OverlayFlags(-3, PSPF_RENDERSTYLE, true);
		A_OverlayRenderstyle(-3, STYLE_ADD);
		
		A_StartSound("mg338/fire", 0, CHANF_OVERLAP, 1);
		A_StartSound("m30/fireadd", 0, CHANF_OVERLAP, 0.75);
		COD_FireBullets("COD_93x74mmR", 1, frandom(-0.1, 0.1), 0, 0, frandom(-0.1, 0.1));
		A_SpawnItemEx("PlayerMuzzleFlash",30,0,45);
		
		//this handles placing the flash correctly
		if(CountInv("AimingToken"))
		{
			A_OverlayOffset(-3, 0 - 18, 32 - 54);
			COD_HandleWeaponFeedback(4, 6, -1.3, frandom(+1.0, -1.0), 0, 0, 0);
			A_ZoomFactor(1.2-0.1);
		}
		else
		{
			A_OverlayOffset(-3, 0 + 12, 32 - 50);
			COD_HandleWeaponFeedback(4, 6, -2.60, frandom(+2.0, -2.0), -3, 0, 0);
			A_ZoomFactor(1-0.1);
		}
		//gunsmoke
		A_TakeInventory("COD_M30_UB", 1);
	}
	
	States
	{
	Deselect:
		TNT1 A 0 A_StartSound("m30/lower", 0, CHANF_OVERLAP, 1);
		TNT1 A 0 A_ZoomFactor(1);
		AA2A HGEDCBA 1;
		PSTG A 0 A_Lower(25);
		Wait;
	User4:
		AA2A HGED 1;
		TNT1 A 0 A_ZoomFactor(1);
		TNT1 A 0 A_TakeInventory("AimingToken");
		AA2A CBA 1;
		Goto NVToggle;
	User2:
		AA2A HGED 1;
		TNT1 A 0 A_ZoomFactor(1);
		TNT1 A 0 A_TakeInventory("AimingToken");
		AA2A CBA 1;
		Goto RadToggle;
	User3:
		TNT1 A 0 A_ZoomFactor(1);
		TNT1 A 0 A_TakeInventory("AimingToken");
		Goto KnifeAttack;
	User1:
		TNT1 A 0;
		AA2F ZYX 1;
		AA2F W 1
		{
			if(CountInv("COD_M30_UBMode"))
			{
				A_TakeInventory("COD_M30_UBMode");
				invoker.AmmoType1 = "Shell";
			}
			else
			{
				A_GiveInventory("COD_M30_UBMode");
				invoker.AmmoType1 = "Clip";
			}
			A_StartSound("uni/slideflip", 0, CHANF_OVERLAP, 1);
		}
		AA2F XYZ 1;
		Goto WeaponReady;
	Select:
		TNT1 A 0
		{
			A_TakeInventory("AimingToken");
			A_TakeInventory("DoAttachment");
		}
		TNT1 A 0 A_Raise(25);
		Wait;
	Ready:
		TNT1 A 0 A_StartSound("m30/raise", 0, CHANF_OVERLAP, 1);
		TNT1 A 6;
		AA2A ABCDEGH 1;
	WeaponReady:
		AA2A F 1 COD_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER1 | WRF_ALLOWUSER2 | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		TNT1 A 0 A_JumpIfInventory("ThrowGrenade", 1, "FragGrenade");
		TNT1 A 0 A_JumpIfInventory("ThrowBang", 1, "StunGrenade");
		Loop;
	WeaponReady2:
		AA2C D 1 COD_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER1 | WRF_ALLOWUSER2 | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		TNT1 A 0 A_JumpIfInventory("ThrowGrenade", 1, "FragGrenade");
		TNT1 A 0 A_JumpIfInventory("ThrowBang", 1, "StunGrenade");
		Loop;
	Fire:
		TNT1 A 0 A_JumpIfInventory("COD_M30_UBMode", 1, "FireUB");
		TNT1 A 0 A_JumpIfInventory("AimingToken", 1, "Fire2");
		TNT1 A 0 A_JumpIf(CountInv("COD_M30_Tube") == 0, "DryFire");
		AA2A F 1;
		AA2B A 1 FireWeapon();
		AA2B B 1;
		TNT1 A 0 A_ZoomFactor(1);
		AA2B CDEFGH 1;
		Goto WeaponReady;
	Fire2:
		TNT1 A 0 A_JumpIf(CountInv("COD_M30_Tube") == 0, "DryFire2");
		AA2C D 1;
		AA2D A 1 FireWeapon();
		AA2D B 1;
		TNT1 A 0 A_ZoomFactor(1.2);
		AA2D CDEFGH 1;
		Goto WeaponReady2;
	FireUB:
		TNT1 A 0 A_JumpIfInventory("AimingToken", 1, "FireUB2");
		TNT1 A 0 A_JumpIf(CountInv("COD_M30_UB") == 0, "DryFire");
		AA2B A 1 FireWeaponUB();
		AA2B B 1;
		TNT1 A 0 A_ZoomFactor(1);
		AA2B CDEFGH 1;
		Goto WeaponReady;
	FireUB2:
		TNT1 A 0 A_JumpIf(CountInv("COD_M30_UB") == 0, "DryFire2");
		AA2D A 1 FireWeaponUB();
		AA2D B 1;
		TNT1 A 0 A_ZoomFactor(1.2);
		AA2D CDEFGH 1;
		Goto WeaponReady2;
	AltFire:
		TNT1 A 0 A_JumpIfInventory("AimingToken", 1, "AltFire2");
		TNT1 A 0 A_GiveInventory("AimingToken");
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP, 1);
		AA2C ABC 1;
		TNT1 A 0 A_ZoomFactor(1.2);
		AA2C EFD 1;
		Goto WeaponReady2;
	AltFire2:
		TNT1 A 0 A_TakeInventory("AimingToken");
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP, 1);
		AA2C DFE 1;
		TNT1 A 0 A_ZoomFactor(1);
		AA2C CBA 1;
		Goto WeaponReady;
	
	DryFire:
		AA2A F 1;
		Goto WeaponReady;
	DryFire2:
		AA2C D 1;
		Goto WeaponReady2;
		
	UnAimReload:
		TNT1 A 0 A_TakeInventory("AimingToken");
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP, 1);
		AA2C DFE 1;
		TNT1 A 0 A_ZoomFactor(1);
		AA2C CBA 1;
	Reload:
		TNT1 A 0 A_JumpIf(CountInv("AimingToken") == 1, "UnAimReload");
		TNT1 A 0 A_JumpIfInventory("COD_M30_UBMode", 1, "Reload2");
		TNT1 A 0 A_JumpIf(CountInv("COD_M30_Tube") == 0 && CountInv("Shell") == 0, "WeaponReadyEmpty");
		TNT1 A 0 A_JumpIf(CountInv("COD_M30_Tube") == 2 || CountInv("Shell") == 0, "WeaponReady");
		TNT1 A 0 A_TakeInventory("AimingToken");
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP, 1);
		AA2E ABCDEF 1;
		TNT1 A 0 A_StartSound("m30/open", 0, CHANF_OVERLAP, 1);
		AA2E GHIJK 1;
		AA2E LMN 2;
		TNT1 A 0 A_StartSound("uni/clothfoleys", 0, CHANF_OVERLAP, 1);
		AA2E OPQR 2;
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP, 1);
		AA2E STUVW 1;
		TNT1 A 0 A_StartSound("uni/cartfoley", 0, CHANF_OVERLAP, 1);
		AA2E XYZ 1;
		AA2F AB 2;
		TNT1 A 0 A_StartSound("m30/shell", 0, CHANF_OVERLAP, 1);
		AA2F C 2;
		AA2F D 3;
		TNT1 A 0 A_StartSound("uni/cartfoley", 0, CHANF_OVERLAP, 1);
		AA2F EF 2;
		TNT1 A 0 A_StartSound("m30/shell", 0, CHANF_OVERLAP, 1);
		AA2F G 2;
		AA2F H 3;
		TNT1 A 0 COD_AmmoIntoMag("COD_M30_Tube", "Shell", 2, 1);
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP, 1);
		AA2F IJKLMNO 1;
		TNT1 A 0 A_StartSound("m30/close", 0, CHANF_OVERLAP, 1);
		AA2F PQRSTUVWXYZ 1;
		Goto WeaponReady;
	Reload2:
		TNT1 A 0 A_JumpIf(CountInv("COD_M30_UB") == 0 && CountInv("Clip") == 0, "WeaponReadyEmpty");
		TNT1 A 0 A_JumpIf(CountInv("COD_M30_UB") == 1 || CountInv("Clip") == 0, "WeaponReady");
		TNT1 A 0 A_TakeInventory("AimingToken");
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP, 1);
		AA2E ABCDEF 1;
		TNT1 A 0 A_StartSound("m30/open", 0, CHANF_OVERLAP, 1);
		AA2E GH 1;
		AA2G ABC 1;
		TNT1 A 0 A_StartSound("uni/cartfoley", 0, CHANF_OVERLAP, 1);
		AA2G DEFG 2;
		AA2G HIJ 1;
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP, 1);
		AA2G KLMNOP 1;
		TNT1 A 0 A_StartSound("uni/cartfoley", 0, CHANF_OVERLAP, 1);
		AA2G QR 2;
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP, 1);
		AA2G STU 2;
		TNT1 A 0 A_StartSound("mosin/shell", 0, CHANF_OVERLAP, 1);
		TNT1 A 0 COD_AmmoIntoMag("COD_M30_UB", "Clip", 1, 1);
		AA2G VW 2;
		TNT1 A 0 A_StartSound("uni/clothfoleys", 0, CHANF_OVERLAP, 1);
		AA2G XYZ 1;
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP, 1);
		AA2F KLMNO 1;
		TNT1 A 0 A_StartSound("m30/close", 0, CHANF_OVERLAP, 1);
		AA2F PQRSTUVWXYZ 1;
		TNT1 A 0 A_JumpIf(CountInv("Shell") == 0, "ReloadEnd");
		Goto WeaponReady;
 	Spawn:
		B001 S -1;
		Stop;
	
	}
}

Class COD_M30_UBMode : Inventory
{
	Default
	{
		Inventory.MaxAmount 1;
	}
}

Class COD_M30_Tube : Ammo
{
	Default
	{
		Inventory.Amount 0;
		Inventory.MaxAmount 2;
		Ammo.BackpackAmount 0;
		Ammo.BackpackMaxAmount 2;
	}
}

Class COD_M30_UB : Ammo
{
	Default
	{
		Inventory.Amount 0;
		Inventory.MaxAmount 1;
		Ammo.BackpackAmount 0;
		Ammo.BackpackMaxAmount 1;
	}
}
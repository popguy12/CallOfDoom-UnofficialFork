class COD_MP155 : CODWeapon
{
 	Default
	{
		Weapon.AmmoType "Shell";
		Weapon.AmmoUse 0;
		Weapon.AmmoGive 4;
		Weapon.AmmoType2 "COD_MP155_Tube";
		Weapon.AmmoUse2 0;
		Weapon.AmmoGive2 0;
		
		Weapon.SlotNumber 3;
		
		/*
		zoom 1.0
		zoomaim 1.2
		zoomdelta 0.1
		zoom tics 2
		*/
		
		Obituary "$OB_MPPISTOL";
		Inventory.Pickupmessage "[3] MP155";
		Tag "MP155";
		Scale 0.16;
		Inventory.PickupSound "weapons/pistol/pickup";
		+WEAPON.NOALERT;
		+WEAPON.AMMO_OPTIONAL;
		
		CODWeapon.PlaySpeed 0.8;
		CODWeapon.SlotType 1;
		CODWeapon.InfoGraphics "ISMP155", "ICMP155N", "ICMP155U", "WLMP155";
	}
	
	action void FireWeapon()
	{
		A_AlertMonsters();
		A_Overlay(-3, "MuzzleBig");
		A_OverlayFlags(-3, PSPF_ALPHA, true);
		A_OverlayFlags(-3, PSPF_RENDERSTYLE, true);
		A_OverlayRenderstyle(-3, STYLE_ADD);
		
		A_StartSound("MP155/fire", 0, CHANF_OVERLAP, 1, ATTN_NORM, 0.75);
		COD_FireBullets("COD_12GAPellet", 9, frandom(-6.0, 6.0), 0, 0, frandom(-4.0, 4.0));
		A_SpawnItemEx("PlayerMuzzleFlash",30,0,45);
		
		//this handles placing the flash correctly
		if(CountInv("AimingToken"))
		{
			A_StartSound("MP155/firemech", 0, CHANF_OVERLAP, 1);
			A_OverlayOffset(-3, 0 - 18, 32 - 54);
			COD_HandleWeaponFeedback(4, 6, -1.3, frandom(+1.1, -1.1), 0, 0, 0);
			A_ZoomFactor(1.2-0.1);
		}
		else
		{
			A_StartSound("MP155/firemech", 0, CHANF_OVERLAP, 0.5);
			A_OverlayOffset(-3, 0 + 12, 32 - 50);
			COD_HandleWeaponFeedback(4, 6, -2.60, frandom(+2.2, -2.2), -3, 0, 0);
			A_ZoomFactor(1-0.1);
		}
		//gunsmoke
		A_TakeInventory("COD_MP155_Tube", 1);
	}
	
	States
	{
	Deselect:
		TNT1 A 0 A_StartSound("MP155/lower", 0, CHANF_OVERLAP, 1);
		TNT1 A 0 A_ZoomFactor(1);
		P55A JIHGEDCBA 1;
		PSTG A 0 A_Lower(25);
		Wait;
	User4:
		P55A JIHGED 1;
		TNT1 A 0 A_ZoomFactor(1);
		TNT1 A 0 A_TakeInventory("AimingToken");
		P55A CBA 1;
		Goto NVToggle;
	User2:
		P55A JIHGED 1;
		TNT1 A 0 A_ZoomFactor(1);
		TNT1 A 0 A_TakeInventory("AimingToken");
		P55A CBA 1;
		Goto RadToggle;
	User3:
		TNT1 A 0 A_ZoomFactor(1);
		TNT1 A 0 A_TakeInventory("AimingToken");
		Goto KnifeAttack;
	Select:
		TNT1 A 0
		{
			A_TakeInventory("AimingToken");
			A_TakeInventory("DoAttachment");
		}
		TNT1 A 0 A_Raise(25);
		Wait;
	Ready:
		TNT1 A 0 A_StartSound("MP155/raise", 0, CHANF_OVERLAP, 1);
		TNT1 A 6;
		P55A ABCDEGHIJ 1;
	WeaponReady:
		P55A F 1 COD_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER2 | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		TNT1 A 0 A_JumpIf(CountInv("COD_MP155_Tube") == 0, "WeaponReadyEmpty");
		TNT1 A 0 A_JumpIfInventory("ThrowGrenade", 1, "FragGrenade");
		TNT1 A 0 A_JumpIfInventory("ThrowBang", 1, "StunGrenade");
		Loop;
	WeaponReady2:
		P55C D 1 COD_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER2 | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		TNT1 A 0 A_JumpIf(CountInv("COD_MP155_Tube") == 0, "WeaponReady2Empty");
		TNT1 A 0 A_JumpIfInventory("ThrowGrenade", 1, "FragGrenade");
		TNT1 A 0 A_JumpIfInventory("ThrowBang", 1, "StunGrenade");
		Loop;
	WeaponReadyEmpty:
		P55B A 1 COD_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER2 | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		TNT1 A 0 A_JumpIfInventory("ThrowGrenade", 1, "FragGrenade");
		TNT1 A 0 A_JumpIfInventory("ThrowBang", 1, "StunGrenade");
		Loop;
	WeaponReady2Empty:
		P55D C 1 COD_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER2 | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		TNT1 A 0 A_JumpIfInventory("ThrowGrenade", 1, "FragGrenade");
		TNT1 A 0 A_JumpIfInventory("ThrowBang", 1, "StunGrenade");
		Loop;
	Fire:
		TNT1 A 0 A_JumpIfInventory("AimingToken", 1, "Fire2");
		TNT1 A 0 A_JumpIf(CountInv("COD_MP155_Tube") == 0, "DryFire");
		P55A F 1;
		TNT1 A 0 FireWeapon();
		P55B AB 1;
		TNT1 A 0 A_ZoomFactor(1);
		P55B CDEF 1;
		TNT1 A 0 A_JumpIf(CountInv("COD_MP155_Tube") == 0, "WeaponReadyEmpty");
		Goto WeaponReady;
	Fire2:
		TNT1 A 0 A_JumpIf(CountInv("COD_MP155_Tube") == 0, "DryFire2");
		P55C D 1;
		TNT1 A 0 FireWeapon();
		P55D AB 1;
		TNT1 A 0 A_ZoomFactor(1.2);
		P55D CDEF 1;
		TNT1 A 0 A_JumpIf(CountInv("COD_MP155_Tube") == 0, "WeaponReady2Empty");
		Goto WeaponReady2;
	AltFire:
		TNT1 A 0 A_JumpIfInventory("AimingToken", 1, "AltFire2");
		TNT1 A 0 A_GiveInventory("AimingToken");
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP, 1);
		P55C ABC 1;
		TNT1 A 0 A_ZoomFactor(1.2);
		P55C EFD 1;
		TNT1 A 0 A_JumpIf(CountInv("COD_MP155_Tube") == 0, "WeaponReady2Empty");
		Goto WeaponReady2;
	AltFire2:
		TNT1 A 0 A_TakeInventory("AimingToken");
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP, 1);
		P55C DFE 1;
		TNT1 A 0 A_ZoomFactor(1);
		P55C CBA 1;
		TNT1 A 0 A_JumpIf(CountInv("COD_MP155_Tube") == 0, "WeaponReadyEmpty");
		Goto WeaponReady;
	
	DryFire:
		P55B A 1;
		Goto WeaponReadyEmpty;
	DryFire2:
		P55D E 1;
		Goto WeaponReady2Empty;
	
	UnAimReload:
		TNT1 A 0 A_TakeInventory("AimingToken");
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP, 1);
		P55C DGFE 1;
		TNT1 A 0 A_ZoomFactor(1);
		P55C CBA 1;
	Reload:
		TNT1 A 0 A_JumpIf(CountInv("AimingToken") == 1, "UnAimReload");
		TNT1 A 0 A_JumpIf(CountInv("COD_MP155_Tube") == 0 && CountInv("Shell") == 0, "WeaponReadyEmpty");
		TNT1 A 0 A_JumpIf(CountInv("COD_MP155_Tube") == 7 || CountInv("Shell") == 0, "WeaponReady");
		TNT1 A 0 A_JumpIf(CountInv("COD_MP155_Tube") == 0, "Reload2");
		TNT1 A 0 A_TakeInventory("AimingToken");
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP, 1);
		P55E ABCDE 2;
		P55E FGHIJK 1;
	ReloadLoop:
		TNT1 A 0 A_StartSound("uni/cartfoley", 0, CHANF_OVERLAP, 1);
		P55E K 4;
		P55E LMNO 2;
		TNT1 A 0 A_StartSound("MP155/shell", 0, CHANF_OVERLAP, 1);
		TNT1 A 0 COD_AmmoIntoMag("COD_MP155_Tube", "Shell", (CountInv("COD_MP155_Tube") + 1), 1);
		P55E PQR 1;
		TNT1 A 0 A_StartSound("uni/clothfoleys", 0, CHANF_OVERLAP, 1);
		P55E STUVWXYZ 1;
		TNT1 A 0 A_JumpIf(CountInv("COD_MP155_Tube") ==	7 || CountInv("Shell") == 0, "ReloadEnd");
		Loop;
	ReloadEnd:
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP, 1);
		P55E KJIHGFEDCBA 1;
		Goto WeaponReady;
	Reload2:
		P55F AB 2;
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP, 1);
		P55F CDE 2;
		P55F FGHIJK 1;
		TNT1 A 0 A_StartSound("uni/cartfoley", 0, CHANF_OVERLAP, 1);
		P55F LMNOPQR 1;
		P55F ST 2;
		TNT1 A 0 A_StartSound("MP155/firstshell", 0, CHANF_OVERLAP, 1);
		TNT1 A 0 COD_AmmoIntoMag("COD_MP155_Tube", "Shell", 1, 1);
		P55F UVW 2;
		TNT1 A 0 A_StartSound("MP155/charge", 0, CHANF_OVERLAP, 1);
		P55F X 2;
		P55F YZ 1;
		TNT1 A 0 A_StartSound("uni/clothfoleys", 0, CHANF_OVERLAP, 1);
		P55G AB 2;
		P55G CDEFGHIJ 1;
		TNT1 A 0 A_JumpIf(CountInv("Shell") == 0, "ReloadEnd");
		Goto ReloadLoop;
 	Spawn:
		B002 I -1;
		Stop;
	
	}
}

Class COD_MP155_Tube : Ammo
{
	Default
	{
		Inventory.Amount 0;
		Inventory.MaxAmount 7;
		Ammo.BackpackAmount 0;
		Ammo.BackpackMaxAmount 7;
	}
}
class COD_M1897 : CODWeapon replaces shotgun
{
 	Default
	{
		Weapon.AmmoType "Shell";
		Weapon.AmmoUse 0;
		Weapon.AmmoGive 4;
		Weapon.AmmoType2 "COD_M1897_Tube";
		Weapon.AmmoUse2 0;
		Weapon.AmmoGive2 0;
		
		Weapon.SlotNumber 3;
		
		/*
		zoom 1.0
		zoomaim 1.2
		zoomdelta 0.1
		zoom tics 2
		*/
		
		Obituary "$OB_MPPISTOL";
		Inventory.Pickupmessage "[3] M1897";
		Tag "M1897";
		Scale 0.16;
		Inventory.PickupSound "weapons/pistol/pickup";
		+WEAPON.NOALERT;
		+WEAPON.AMMO_OPTIONAL;
	}
	
	action void FireWeapon()
	{
		A_AlertMonsters();
		A_Overlay(-3, "MuzzleBig");
		A_OverlayFlags(-3, PSPF_RENDERSTYLE, true);
		A_OverlayRenderstyle(-3, STYLE_ADD);
		
		A_StartSound("trn/fire", 0, CHANF_OVERLAP, 1);
		COD_FireBullets("COD_12GAPellet", 9, frandom(-6.0, 6.0), 0, 0, frandom(-4.0, 4.0));
		
		//this handles placing the flash correctly
		if(CountInv("AimingToken"))
		{
			A_OverlayOffset(-3, 0 - 18, 32 - 54);
			COD_HandleWeaponFeedback(4, 6, -1.3, frandom(+1.0, -1.0));
			A_ZoomFactor(1.2-0.1);
		}
		else
		{
			A_OverlayOffset(-3, 0 + 12, 32 - 50);
			COD_HandleWeaponFeedback(4, 6, -2.60, frandom(+2.0, -2.0));
			A_ZoomFactor(1-0.1);
		}
		//gunsmoke
		A_TakeInventory("COD_M1897_Tube", 1);
	}
	
	States
	{
	Deselect:
		TNT1 A 0 A_StartSound("trn/lower", 0, CHANF_OVERLAP, 1);
		TNT1 A 0 A_ZoomFactor(1);
		TRNA JIHGEDCBA 1;
		TNT1 A 3;
		PSTG A 0 A_Lower(25);
		Wait;
	User4:
		TRNA JIHGED 1;
		TNT1 A 0 A_ZoomFactor(1);
		TNT1 A 0 A_TakeInventory("AimingToken");
		TRNA CBA 1;
		Goto NVToggle;
	User3:
		TNT1 A 0 A_ZoomFactor(1);
		TNT1 A 0 A_TakeInventory("AimingToken");
		Goto KnifeAttack;
	Select:
		TNT1 A 0
		{
			A_TakeInventory("AimingToken");
		}
		TNT1 A 0 A_Raise(25);
		Wait;
	Ready:
		TNT1 A 0 A_StartSound("trn/raise", 0, CHANF_OVERLAP, 1);
		TRNA ABCDEGHIJ 1;
	WeaponReady:
		TRNA F 1 COD_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		TNT1 A 0 A_JumpIf(CountInv("COD_M1897_Tube") == 0, "WeaponReadyEmpty");
		TNT1 A 0 A_JumpIfInventory("ThrowGrenade", 1, "FragGrenade");
		TNT1 A 0 A_JumpIfInventory("ThrowBang", 1, "StunGrenade");
		Loop;
	WeaponReady2:
		TRNC D 1 COD_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		TNT1 A 0 A_JumpIf(CountInv("COD_M1897_Tube") == 0, "WeaponReady2Empty");
		TNT1 A 0 A_JumpIfInventory("ThrowGrenade", 1, "FragGrenade");
		TNT1 A 0 A_JumpIfInventory("ThrowBang", 1, "StunGrenade");
		Loop;
	WeaponReadyEmpty:
		TRNB A 1 COD_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		TNT1 A 0 A_JumpIfInventory("ThrowGrenade", 1, "FragGrenade");
		TNT1 A 0 A_JumpIfInventory("ThrowBang", 1, "StunGrenade");
		Loop;
	WeaponReady2Empty:
		TRND E 1 COD_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		TNT1 A 0 A_JumpIfInventory("ThrowGrenade", 1, "FragGrenade");
		TNT1 A 0 A_JumpIfInventory("ThrowBang", 1, "StunGrenade");
		Loop;
	Fire:
		TNT1 A 0 A_JumpIfInventory("AimingToken", 1, "Fire2");
		TNT1 A 0 A_JumpIf(CountInv("COD_M1897_Tube") == 0, "DryFire");
		TRNA F 1;
		TNT1 A 0 FireWeapon();
		TRNB AB 1;
		TNT1 A 0 A_ZoomFactor(1);
		TRNB CDEF 1;
		TNT1 A 0 A_JumpIf(CountInv("COD_M1897_Tube") == 0, "WeaponReadyEmpty");
		TNT1 A 0 A_StartSound("trn/pumpback", 0, CHANF_OVERLAP, 1);
		TRNB GHI 1;
		TNT1 A 0 A_StartSound("uni/clothfoleys", 0, CHANF_OVERLAP, 1);
		TRNB JKLMNOPQ 1;
		TNT1 A 0 A_StartSound("uni/clothfoleys", 0, CHANF_OVERLAP, 1);
		TNT1 A 0 A_StartSound("trn/pumpforward", 0, CHANF_OVERLAP, 1);
		TRNB RSTUVWXY 1;
		Goto WeaponReady;
	Fire2:
		TNT1 A 0 A_JumpIf(CountInv("COD_M1897_Tube") == 0, "DryFire2");
		TRNC D 1;
		TNT1 A 0 FireWeapon();
		TRND AB 1;
		TNT1 A 0 A_ZoomFactor(1.2);
		TRND CD 1;
		TNT1 A 0 A_JumpIf(CountInv("COD_M1897_Tube") == 0, "WeaponReady2Empty");
		TNT1 A 0 A_StartSound("trn/pumpback", 0, CHANF_OVERLAP, 1);
		TRND EFGH 1;
		TNT1 A 0 A_StartSound("uni/clothfoleys", 0, CHANF_OVERLAP, 1);
		TRND I 3;
		TNT1 A 0 A_StartSound("trn/pumpforward", 0, CHANF_OVERLAP, 1);
		TRND JKLMN 1;
		Goto WeaponReady2;
	AltFire:
		TNT1 A 0 A_JumpIfInventory("AimingToken", 1, "AltFire2");
		TNT1 A 0 A_GiveInventory("AimingToken");
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP, 1);
		TRNC ABC 1;
		TNT1 A 0 A_ZoomFactor(1.2);
		TRNC EFGD 1;
		TNT1 A 0 A_JumpIf(CountInv("COD_M1897_Tube") == 0, "WeaponReady2Empty");
		Goto WeaponReady2;
	AltFire2:
		TNT1 A 0 A_TakeInventory("AimingToken");
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP, 1);
		TRNC DGFE 1;
		TNT1 A 0 A_ZoomFactor(1);
		TRNC CBA 1;
		TNT1 A 0 A_JumpIf(CountInv("COD_M1897_Tube") == 0, "WeaponReadyEmpty");
		Goto WeaponReady;
	
	DryFire:
		TRNB A 1;
		Goto WeaponReadyEmpty;
	DryFire2:
		TRND E 1;
		Goto WeaponReady2Empty;
	
	UnAimReload:
		TNT1 A 0 A_TakeInventory("AimingToken");
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP, 1);
		TRNC DGFE 1;
		TNT1 A 0 A_ZoomFactor(1);
		TRNC CBA 1;
	Reload:
		TNT1 A 0 A_JumpIf(CountInv("AimingToken") == 1, "UnAimReload");
		TNT1 A 0 A_JumpIf(CountInv("COD_M1897_Tube") == 0 && CountInv("Shell") == 0, "WeaponReadyEmpty");
		TNT1 A 0 A_JumpIf(CountInv("COD_M1897_Tube") == 6 || CountInv("Shell") == 0, "WeaponReady");
		TNT1 A 0 A_JumpIf(CountInv("COD_M1897_Tube") == 0, "Reload2");
		TNT1 A 0 A_TakeInventory("AimingToken");
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP, 1);
		TRNE ABCDEFGHIJKL 1;
	ReloadLoop:
		TNT1 A 0 A_StartSound("uni/cartfoley", 0, CHANF_OVERLAP, 1);
		TRNE MN 1;
		TNT1 A 0 A_StartSound("trn/shell", 0, CHANF_OVERLAP, 1);
		TRNE OPQRS 1;
		TNT1 A 0 COD_AmmoIntoMag("COD_M1897_Tube", "Shell", (CountInv("COD_M1897_Tube") + 1), 1);
		TNT1 A 0 A_StartSound("uni/clothfoleys", 0, CHANF_OVERLAP, 1);
		TRNE TUVWX 1;
		TNT1 A 0 A_JumpIf(CountInv("COD_M1897_Tube") ==	6 || CountInv("Shell") == 0, "ReloadEnd");
		Loop;
	ReloadEnd:
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP, 1);
		TRNE LKJIHGFEDCBA 1;
		Goto WeaponReady;
	Reload2:
		TRNG AB 1;
		TNT1 A 0 A_StartSound("trn/pumpback", 0, CHANF_OVERLAP, 1);
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP, 1);
		TRNG CDEFG 1;
		TNT1 A 0 A_StartSound("uni/cartfoley", 0, CHANF_OVERLAP, 1);
		TRNG HIJKLM 1;
		TNT1 A 0 A_StartSound("trn/shell", 0, CHANF_OVERLAP, 1);
		TNT1 A 0 COD_AmmoIntoMag("COD_M1897_Tube", "Shell", 1, 1);
		TRNG NOPQRS 1;
		TNT1 A 0 A_StartSound("uni/clothfoleys", 0, CHANF_OVERLAP, 1);
		TRNG TUVW 1;
		TNT1 A 0 A_StartSound("trn/pumpforward", 0, CHANF_OVERLAP, 1);
		TRNG XYZ 1;
		TRNH ABCDE 1;
		TNT1 A 0 A_JumpIf(CountInv("Shell") == 0, "ReloadEnd");
		Goto ReloadLoop;
 	Spawn:
		B002 I -1;
		Stop;
	
	}
}

Class COD_M1897_Tube : Ammo
{
	Default
	{
		Inventory.Amount 0;
		Inventory.MaxAmount 6;
		Ammo.BackpackAmount 0;
		Ammo.BackpackMaxAmount 6;
	}
}
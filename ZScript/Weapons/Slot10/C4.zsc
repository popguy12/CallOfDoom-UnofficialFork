class COD_C4Thrower : CODWeapon
{
 	Default
	{
		Weapon.AmmoType "COD_C4Ammo";
		Weapon.AmmoUse 0;
		Weapon.AmmoGive 0;
		
		Weapon.SlotNumber 0;
		Weapon.SlotPriority 2;
		
		/*
		zoom 1.0
		zoomaim 1.2
		zoomdelta 0.01
		zoom tics 1
		*/
		
		Obituary "$OB_MPPISTOL";
		Inventory.Pickupmessage "[0] C4 Detonator";
		Tag "C4 Detonator`";
		Scale 0.16;
		Inventory.PickupSound "weapons/pistol/pickup";
		+WEAPON.NOALERT;
		+WEAPON.AMMO_OPTIONAL;
		+WEAPON.NOAUTOFIRE;
		+INVENTORY.UNDROPPABLE;
		
		CODWeapon.InfoGraphics "ISC4", "ICUSSWN", "ICUSSWU", "WLC4";
	}
	
	action void FireWeapon()
	{
	}
	
	States
	{
	
	Deselect:
		TNT1 A 0 A_StartSound("uni/ClothFoleyM", 0);
		FRGF GFEDCBA 1;
		PSTG A 0 A_Lower(25);
		Wait;
	User4:
		TNT1 A 0 A_ZoomFactor(1);
		VSTA CBA 2;
		Goto NVToggle;
	User2:
		TNT1 A 0 A_ZoomFactor(1);
		VSTA CBA 2;
		Goto RadToggle;
	User3:
		TNT1 A 0 A_ZoomFactor(1);
		Goto KnifeAttack;
	Select:
		TNT1 A 0
		{
			A_TakeInventory("MedicalToken");
		}
		TNT1 A 0 A_Raise(25);
		Wait;
	Ready:
		TNT1 A 0;
		TNT1 A 0 A_StartSound("C4/wep/up", 0);
		FRGF ABCDEFG 1;
	WeaponReady:
		FRGF F 1 COD_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER2 | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		TNT1 A 0 A_JumpIfInventory("ThrowGrenade", 1, "FragGrenade");
		TNT1 A 0 A_JumpIfInventory("ThrowBang", 1, "StunGrenade");
		Loop;
	WeaponReadyMedical:
		TNT1 A 1 COD_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER2 | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		Loop;
	Fire:
		FRGF GHIJ 1;
		TNT1 A 0 A_GiveInventory("BlowUpToken", 1);
		TNT1 A 0 A_StartSound("C4/wep/click", 0);
		FRGF JIHG 1;
		TNT1 A 0 A_TakeInventory("BlowUpToken", 1);
		Goto WeaponReady;
	AltFire:
		TNT1 A 0;
		TNT1 A 0 A_JumpIf(CountInv("COD_C4Ammo") < 1, "DryFire");
		FRGE ABC 1;
		TNT1 A 0 A_StartSound("C4/wep/throw", 0);
		TNT1 A 0 A_FireCustomMissile("COD_C4Charge");
		TNT1 A 0 A_TakeInventory("COD_C4Ammo", 1);
		FRGE DEFGHI 1;
		Goto WeaponReady;
	
	Reload:
	DryFire:
		FRGF F 1;
		Goto WeaponReady;
	
 	Spawn:
		MDBG B -1;
		Stop;
	
	}
}

Class BlowUpToken : Inventory
{
	Default
	{
		Inventory.MaxAmount 1;
	}
}

Class COD_C4Ammo : Ammo replaces RocketAmmo
{
	Default
	{
		Inventory.Amount 1;
		Inventory.MaxAmount 5;
		Ammo.BackpackAmount 2;
		Ammo.BackpackMaxAmount 10;
		
		Scale 0.25;
	}
	
	States
	{
		Spawn:
			C4XA A -1;
			Stop;
	}
}
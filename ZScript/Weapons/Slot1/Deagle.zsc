class COD_Deagle : CODWeapon
{
 	Default
	{
		Weapon.AmmoType "Clip";
		Weapon.AmmoUse 0;
		Weapon.AmmoGive 20;
		Weapon.AmmoType2 "COD_Deagle_Mag";
		Weapon.AmmoUse2 0;
		Weapon.AmmoGive2 0;
		
		Weapon.SlotNumber 1;
		
		/*
		zoom 1.0
		zoomaim 2.0
		zoomdelta 0.02
		zoom tics 2
		*/
		
		Obituary "$OB_MPPISTOL";
		Inventory.Pickupmessage "[1] Desert Eagle";
		Tag "Desert Eagle";
		Scale 0.16;
		Inventory.PickupSound "weapons/pistol/pickup";
		+WEAPON.NOALERT;
		+WEAPON.AMMO_OPTIONAL;
		+WEAPON.NOAUTOFIRE;
		
		CODWeapon.PlaySpeed 0.95;
		CODWeapon.SlotType 2;
		CODWeapon.InfoGraphics "ISDE44", "ICDEN", "ICDEU", "WLDE";
	}
	
	action void FireWeapon()
	{
		A_AlertMonsters();
		A_Overlay(-3, "MuzzleMedium");
		A_OverlayFlags(-3, PSPF_ALPHA, true);
		A_OverlayFlags(-3, PSPF_RENDERSTYLE, true);
		A_OverlayRenderstyle(-3, STYLE_ADD);
		
		A_StartSound("deagle/fire", 0, CHANF_OVERLAP, 0.75);
		A_StartSound("deagle/fireadd", 0, CHANF_OVERLAP, 1);
		COD_FireBullets("COD_44Magnum", 1, frandom(-0.1, 0.1), 0, 0, frandom(-0.1, 0.1));
		A_SpawnItemEx("PlayerMuzzleFlash",30,0,45);
		
		//this handles placing the flash correctly
		if(CountInv("AimingToken"))
		{
			A_OverlayOffset(-3, 0 - 18, 32 - 54);
			COD_HandleWeaponFeedback(3, 7, -1.20, frandom(+0.60, -0.60), 0, 0, 0);
			A_ZoomFactor(2.0-0.02);
		}
		else
		{
			A_OverlayOffset(-3, 0 + 24, 32 - 42);
			COD_HandleWeaponFeedback(3, 7, -2.40, frandom(+1.2, -1.2), -5, 0, 0);
			A_ZoomFactor(1-0.02);
		}
		//gunsmoke
		A_TakeInventory("COD_Deagle_Mag", 1);
	}
	
	States
	{
	
	Deselect:
		TNT1 A 0 A_ZoomFactor(1);
		TNT1 A 0 A_StartSound("deagle/lower", 0, CHANF_OVERLAP, 1);
		DE5A FEDCBA 1;
		PSTG A 0 A_Lower(25);
		Wait;
	User4:
		DE5A FED 1;
		TNT1 A 0 A_ZoomFactor(1);
		TNT1 A 0 A_TakeInventory("AimingToken");
		DE5A CBA 1;
		Goto NVToggle;
	User2:
		DE5A FED 1;
		TNT1 A 0 A_ZoomFactor(1);
		TNT1 A 0 A_TakeInventory("AimingToken");
		DE5A CBA 1;
		Goto RadToggle;
	User3:
		TNT1 A 0 A_ZoomFactor(1);
		TNT1 A 0 A_TakeInventory("AimingToken");
		Goto KnifeAttack;
	Select:
		TNT1 A 0
		{
			A_TakeInventory("AimingToken");
		}
		TNT1 A 0 A_Raise(25);
		Wait;
	Ready:
		TNT1 A 0 A_StartSound("deagle/raise", 0, CHANF_OVERLAP, 1);
		DE5A ABCDEF 1;
	WeaponReady:
		DE5A F 1 COD_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER2 | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		TNT1 A 0 A_JumpIf(CountInv("COD_Deagle_Mag") == 0, "WeaponReadyEmpty");
		TNT1 A 0 A_JumpIfInventory("ThrowGrenade", 1, "FragGrenade");
		TNT1 A 0 A_JumpIfInventory("ThrowBang", 1, "StunGrenade");
		Loop;
	WeaponReady2:
		DE5C D 1 COD_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER2 | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		TNT1 A 0 A_JumpIf(CountInv("COD_Deagle_Mag") == 0, "WeaponReady2Empty");
		TNT1 A 0 A_JumpIfInventory("ThrowGrenade", 1, "FragGrenade");
		TNT1 A 0 A_JumpIfInventory("ThrowBang", 1, "StunGrenade");
		Loop;
	WeaponReadyEmpty:
		DE5B A 1 COD_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER2 | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		TNT1 A 0 A_JumpIfInventory("ThrowGrenade", 1, "FragGrenade");
		TNT1 A 0 A_JumpIfInventory("ThrowBang", 1, "StunGrenade");
		Loop;
	WeaponReady2Empty:
		DE5D A 1 COD_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER2 | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		TNT1 A 0 A_JumpIfInventory("ThrowGrenade", 1, "FragGrenade");
		TNT1 A 0 A_JumpIfInventory("ThrowBang", 1, "StunGrenade");
		Loop;
	Fire:
		TNT1 A 0 A_JumpIfInventory("AimingToken", 1, "Fire2");
		TNT1 A 0 A_JumpIf(CountInv("COD_Deagle_Mag") == 0, "DryFire");
		DE5A F 1;
		DE5B A 1 FireWeapon();
		DE5B B 1;
		TNT1 A 0 A_ZoomFactor(1);
		DE5B C 1;
		TNT1 A 0 A_JumpIf(CountInv("COD_Deagle_Mag") == 0, "WeaponReadyEmpty");
		DE5B DEFG 1;
		DE5B HIJK 1 COD_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER2 | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		Goto WeaponReady;
	Fire2:
		TNT1 A 0 A_JumpIf(CountInv("COD_Deagle_Mag") == 0, "DryFire2");
		DE5C D 1;
		DE5D A 1 FireWeapon();
		DE5D B 1;
		TNT1 A 0 A_ZoomFactor(2.0);
		DE5D C 1;
		TNT1 A 0 A_JumpIf(CountInv("COD_Deagle_Mag") == 0, "WeaponReady2Empty");
		DE5D DEFG 1;
		DE5D HIJK 1 COD_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER2 | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		Goto WeaponReady2;
	AltFire:
		TNT1 A 0 A_JumpIfInventory("AimingToken", 1, "AltFire2");
		TNT1 A 0 A_GiveInventory("AimingToken");
		TNT1 A 0 A_StartSound("uni/clothfoleys", 0, CHANF_OVERLAP);
		DE5C AB 1;
		TNT1 A 0 A_ZoomFactor(2.0);
		DE5C CED 1;
		TNT1 A 0 A_JumpIf(CountInv("COD_Deagle_Mag") == 0, "WeaponReady2Empty");
		Goto WeaponReady2;
	AltFire2:
		TNT1 A 0 A_TakeInventory("AimingToken");
		TNT1 A 0 A_StartSound("uni/clothfoleys", 0, CHANF_OVERLAP);
		DE5C DE 1;
		TNT1 A 0 A_ZoomFactor(1);
		DE5C CBA 1;
		TNT1 A 0 A_JumpIf(CountInv("COD_Deagle_Mag") == 0, "WeaponReadyEmpty");
		Goto WeaponReady;
	
	DryFire:
		DE5B A 1;
		Goto WeaponReadyEmpty;
	DryFire2:
		DE5D A 1;
		Goto WeaponReady2Empty;
	
	UnAimReload:
		TNT1 A 0 A_TakeInventory("AimingToken");
		DE5C DE 1;
		TNT1 A 0 A_ZoomFactor(1);
		DE5C CBA 1;
	Reload:
		TNT1 A 0 A_JumpIf(CountInv("AimingToken") == 1, "UnAimReload");
		TNT1 A 0 A_JumpIf(CountInv("COD_Deagle_Mag") == 0 && CountInv("Clip") == 0, "WeaponReadyEmpty");
		TNT1 A 0 A_JumpIf(CountInv("COD_Deagle_Mag") == 8 || CountInv("Clip") == 0, "WeaponReady");
		TNT1 A 0 A_JumpIf(CountInv("COD_Deagle_Mag") == 0, "Reload2");
		TNT1 A 0 A_TakeInventory("AimingToken");
		TNT1 A 0 A_StartSound("uni/clothfoleym");
		DE5E ABCD 1;
		TNT1 A 0 A_StartSound("deagle/emptymagout", 0, CHANF_OVERLAP, 1);
		DE5E EFF 1;
		TNT1 A 0 A_StartSound("deagle/magout", 0, CHANF_OVERLAP, 1);
		DE5E GGHIJKLMN 1;
		TNT1 A 0 A_StartSound("uni/clothfoleys", 0, CHANF_OVERLAP, 1);
		DE5E OPQRST 1;
		TNT1 A 0 A_StartSound("uni/magfol", 0, CHANF_OVERLAP, 1);
		DE5E UVWXYZ 1;
		TNT1 A 0 A_StartSound("deagle/magin", 0, CHANF_OVERLAP, 1);
		DE5F ABCD 1;
		TNT1 A 0 COD_AmmoIntoMag("COD_Deagle_Mag", "Clip", 8, 1);
		DE5F EFGHHIJ 1;
		TNT1 A 0 A_StartSound("uni/clothfoleys", 0, CHANF_OVERLAP, 1);
		DE5F KLMNO 1;
		Goto WeaponReady;
	Reload2:
		TNT1 A 0 A_TakeInventory("AimingToken");
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP);
		DE5G ABCDEFG 1;
		TNT1 A 0 A_StartSound("deagle/emptymagout", 0, CHANF_OVERLAP, 1);
		DE5G HIJ 2;
		TNT1 A 0 A_StartSound("deagle/magout", 0, CHANF_OVERLAP, 1);
		DE5G KLMN 1;
		TNT1 A 0 A_StartSound("uni/clothfoleys", 0, CHANF_OVERLAP, 1);
		DE5G OOOPP 1;
		TNT1 A 0 A_StartSound("uni/magfol", 0, CHANF_OVERLAP, 1);
		DE5G QRST 2;
		TNT1 A 0 A_StartSound("deagle/emptymagin", 0, CHANF_OVERLAP, 1);
		DE5G UVWXYZ 2;
		TNT1 A 0 COD_AmmoIntoMag("COD_Deagle_Mag", "Clip", 7, 1);
		DE5H ABCD 2;
		TNT1 A 0 A_StartSound("uni/clothfoley", 0, CHANF_OVERLAP, 1);
		DE5H EFG 1;
		DE5H HI 2;
		TNT1 A 0 A_StartSound("deagle/slide", 0, CHANF_OVERLAP, 1);
		DE5H JKLMNOPQR 2;
		Goto WeaponReady;
 	Spawn:
		B002 U -1;
		Stop;
	
	}
}

Class COD_Deagle_Mag : Ammo
{
	Default
	{
		Inventory.Amount 0;
		Inventory.MaxAmount 8;
		Ammo.BackpackAmount 0;
		Ammo.BackpackMaxAmount 8;
	}
}
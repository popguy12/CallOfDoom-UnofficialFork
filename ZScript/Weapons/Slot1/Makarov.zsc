class COD_Makarov : CODWeapon replaces pistol
{
 	Default
	{
		Weapon.AmmoType "Clip";
		Weapon.AmmoUse 0;
		Weapon.AmmoGive 20;
		Weapon.AmmoType2 "COD_Makarov_Mag";
		Weapon.AmmoUse2 0;
		Weapon.AmmoGive2 0;
		
		Weapon.SlotNumber 1;
		
		/*
		zoom 1.0
		zoomaim 1.2
		zoomdelta 0.01
		zoom tics 1
		*/
		
		Obituary "$OB_MPPISTOL";
		Inventory.Pickupmessage "[1] Makarov";
		Tag "Makarov";
		Scale 0.16;
		Inventory.PickupSound "weapons/pistol/pickup";
		+WEAPON.NOALERT;
		+WEAPON.AMMO_OPTIONAL;
		+WEAPON.NOAUTOFIRE;
	}
	
	action void FireWeapon()
	{
		A_AlertMonsters();
		A_Overlay(-3, "MuzzleSmall");
		A_OverlayFlags(-3, PSPF_RENDERSTYLE, true);
		A_OverlayRenderstyle(-3, STYLE_ADD);
		
		A_StartSound("pm/fire1", 0, CHANF_OVERLAP, 1);
		COD_FireBullets("COD_9x18mm", 1, frandom(-0.1, 0.1), 0, 0, frandom(-0.1, 0.1));
		
		//this handles placing the flash correctly
		if(CountInv("AimingToken"))
		{
			A_OverlayOffset(-3, 0 - 18, 32 - 54);
			COD_HandleWeaponFeedback(2, 3, -0.20, frandom(+0.15, -0.15), 0, 0, 0);
			A_ZoomFactor(1.2-0.01);
		}
		else
		{
			A_OverlayOffset(-3, 0 + 24, 32 - 42);
			COD_HandleWeaponFeedback(2, 3, -0.40, frandom(+0.30, -0.30), -5, 0, 0);
			A_ZoomFactor(1-0.01);
		}
		//gunsmoke
		A_TakeInventory("COD_Makarov_Mag", 1);
	}
	
	States
	{
	
	Deselect:
		TNT1 A 0 A_ZoomFactor(1);
		TNT1 A 0 A_StartSound("pm/lower", 0, CHANF_OVERLAP, 1);
		P9PA EDCBA 1;
		PSTG A 0 A_Lower(25);
		Wait;
	User4:
		P9PA ED 1;
		TNT1 A 0 A_ZoomFactor(1);
		TNT1 A 0 A_TakeInventory("AimingToken");
		P9PA CBA 1;
		Goto NVToggle;
	User3:
		TNT1 A 0 A_ZoomFactor(1);
		TNT1 A 0 A_TakeInventory("AimingToken");
		Goto KnifeAttack;
	Select:
		TNT1 A 0
		{
			A_TakeInventory("AimingToken");
		}
		TNT1 A 0 A_Raise(25);
		Wait;
	Ready:
		TNT1 A 0 A_StartSound("pm/raise", 0, CHANF_OVERLAP, 1);
		P9PA ABCDE 1;
	WeaponReady:
		P9PA F 1 COD_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		TNT1 A 0 A_JumpIf(CountInv("COD_Makarov_Mag") == 0, "WeaponReadyEmpty");
		TNT1 A 0 A_JumpIfInventory("ThrowGrenade", 1, "FragGrenade");
		TNT1 A 0 A_JumpIfInventory("ThrowBang", 1, "StunGrenade");
		Loop;
	WeaponReady2:
		P9PC D 1 COD_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		TNT1 A 0 A_JumpIf(CountInv("COD_Makarov_Mag") == 0, "WeaponReady2Empty");
		TNT1 A 0 A_JumpIfInventory("ThrowGrenade", 1, "FragGrenade");
		TNT1 A 0 A_JumpIfInventory("ThrowBang", 1, "StunGrenade");
		Loop;
	WeaponReadyEmpty:
		P9PB B 1 COD_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		TNT1 A 0 A_JumpIfInventory("ThrowGrenade", 1, "FragGrenade");
		TNT1 A 0 A_JumpIfInventory("ThrowBang", 1, "StunGrenade");
		Loop;
	WeaponReady2Empty:
		P9PD B 1 COD_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		TNT1 A 0 A_JumpIfInventory("ThrowGrenade", 1, "FragGrenade");
		TNT1 A 0 A_JumpIfInventory("ThrowBang", 1, "StunGrenade");
		Loop;
	Fire:
		TNT1 A 0 A_JumpIfInventory("AimingToken", 1, "Fire2");
		TNT1 A 0 A_JumpIf(CountInv("COD_Makarov_Mag") == 0, "DryFire");
		P9PA F 1;
		TNT1 A 0 FireWeapon();
		P9PB A 1;
		TNT1 A 0 A_ZoomFactor(1);
		P9PB B 1;
		TNT1 A 0 A_JumpIf(CountInv("COD_Makarov_Mag") == 0, "WeaponReadyEmpty");
		P9PB C 1;
		P9PB DEF 1 COD_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		Goto WeaponReady;
	Fire2:
		TNT1 A 0 A_JumpIf(CountInv("COD_Makarov_Mag") == 0, "DryFire2");
		P9PC D 1;
		TNT1 A 0 FireWeapon();
		P9PD A 1;
		TNT1 A 0 A_ZoomFactor(1.2);
		P9PD B 1;
		TNT1 A 0 A_JumpIf(CountInv("COD_Makarov_Mag") == 0, "WeaponReady2Empty");
		P9PD C 1;
		P9PD DEF 1 COD_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		Goto WeaponReady2;
	AltFire:
		TNT1 A 0 A_JumpIfInventory("AimingToken", 1, "AltFire2");
		TNT1 A 0 A_GiveInventory("AimingToken");
		TNT1 A 0 A_StartSound("uni/clothfoleys", 0, CHANF_OVERLAP);
		P9PC AB 1;
		TNT1 A 0 A_ZoomFactor(1.2);
		P9PC CEF 1;
		TNT1 A 0 A_JumpIf(CountInv("COD_Makarov_Mag") == 0, "WeaponReady2Empty");
		Goto WeaponReady2;
	AltFire2:
		TNT1 A 0 A_TakeInventory("AimingToken");
		TNT1 A 0 A_StartSound("uni/clothfoleys", 0, CHANF_OVERLAP);
		P9PC FE 1;
		TNT1 A 0 A_ZoomFactor(1);
		P9PC CBA 1;
		TNT1 A 0 A_JumpIf(CountInv("COD_Makarov_Mag") == 0, "WeaponReadyEmpty");
		Goto WeaponReady;
	
	DryFire:
		P9PB B 1;
		Goto WeaponReadyEmpty;
	DryFire2:
		P9PD A 1;
		Goto WeaponReady2Empty;
	
	UnAimReload:
		TNT1 A 0 A_TakeInventory("AimingToken");
		P9PC FE 1;
		TNT1 A 0 A_ZoomFactor(1);
		P9PC CBA 1;
	Reload:
		TNT1 A 0 A_JumpIf(CountInv("AimingToken") == 1, "UnAimReload");
		TNT1 A 0 A_JumpIf(CountInv("COD_Makarov_Mag") == 0 && CountInv("Clip") == 0, "WeaponReadyEmpty");
		TNT1 A 0 A_JumpIf(CountInv("COD_Makarov_Mag") == 9 || CountInv("Clip") == 0, "WeaponReady");
		TNT1 A 0 A_JumpIf(CountInv("COD_Makarov_Mag") == 0, "Reload2");
		TNT1 A 0 A_TakeInventory("AimingToken");
		TNT1 A 0 A_StartSound("uni/clothfoleym");
		P9PE ABCDEFGHIJK 1;
		TNT1 A 0 A_StartSound("pm/clipout", 0, CHANF_OVERLAP);
		P9PE LMNOPQRSTU 1;
		TNT1 A 0 A_StartSound("uni/magfol", 0, CHANF_OVERLAP);
		P9PE VWXYZ 1;
		P9PF ABCDEFGH 1;
		TNT1 A 0 A_StartSound("pm/clipin", 0, CHANF_OVERLAP);
		P9PF IJKL 1;
		TNT1 A 0 COD_AmmoIntoMag("COD_Makarov_Mag", "Clip", 9, 1);
		P9PF MNOPQRSTU 1;
		Goto WeaponReady;
	Reload2:
		TNT1 A 0 A_TakeInventory("AimingToken");
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP);
		P9PG ABCDEFG 1;
		TNT1 A 0 A_StartSound("pm/clipout", 0, CHANF_OVERLAP);
		P9PG HIJKLM 1;
		TNT1 A 0 A_StartSound("pm/clipout2", 0, CHANF_OVERLAP);
		TNT1 A 0 A_StartSound("uni/clothfoleys", 0, CHANF_OVERLAP);
		P9PG NO 1;
		TNT1 A 0 A_StartSound("uni/magfol", 0, CHANF_OVERLAP);
		P9PG PQRSTUVWXYZ 1;
		P9PH ABC 1;
		TNT1 A 0 A_StartSound("pm/clipin", 0, CHANF_OVERLAP);
		P9PH DEF 1;
		TNT1 A 0 COD_AmmoIntoMag("COD_Makarov_Mag", "Clip", 8 , 1);
		P9PH GH 1;
		TNT1 A 0 A_StartSound("pm/lock", 0, CHANF_OVERLAP);
		P9PH IJKLMNOPQRSTU 1;
		Goto WeaponReady;
 	Spawn:
		B003 S -1;
		Stop;
	
	}
}

Class COD_Makarov_Mag : Ammo
{
	Default
	{
		Inventory.Amount 0;
		Inventory.MaxAmount 9;
		Ammo.BackpackAmount 0;
		Ammo.BackpackMaxAmount 9;
	}
}
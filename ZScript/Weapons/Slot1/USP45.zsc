class COD_USP45 : CODWeapon
{
 	Default
	{
		Weapon.AmmoType "Clip";
		Weapon.AmmoUse 0;
		Weapon.AmmoGive 20;
		Weapon.AmmoType2 "COD_USP45_Mag";
		Weapon.AmmoUse2 0;
		Weapon.AmmoGive2 0;
		
		Weapon.SlotNumber 1;
		
		/*
		zoom 1.0
		zoomaim 1.2
		zoomdelta 0.01
		zoom tics 1
		*/
		
		Obituary "$OB_MPPISTOL";
		Inventory.Pickupmessage "[1] USP-45";
		Tag "USP-45";
		Scale 0.16;
		Inventory.PickupSound "weapons/pistol/pickup";
		+WEAPON.NOALERT;
		+WEAPON.AMMO_OPTIONAL;
		+WEAPON.NOAUTOFIRE;
		
		CODWeapon.PlaySpeed 0.95;
		CODWeapon.SlotType 2;
		CODWeapon.InfoGraphics "ISUSP45", "ICW99N", "ICW99U", "WLW99";
	}
	
	action void FireWeapon()
	{
		/*A_AlertMonsters();
		A_Overlay(-3, "MuzzleSmall");
		A_OverlayFlags(-3, PSPF_ALPHA, true);
		A_OverlayFlags(-3, PSPF_RENDERSTYLE, true);
		A_OverlayRenderstyle(-3, STYLE_ADD);*/
		
		A_StartSound("usp45/fire", 0, CHANF_OVERLAP, 1);
		COD_FireBullets("COD_45ACP", 1, frandom(-0.1, 0.1), 0, 0, frandom(-0.1, 0.1));
		//A_SpawnItemEx("PlayerMuzzleFlash",30,0,45);
		
		//this handles placing the flash correctly
		if(CountInv("AimingToken"))
		{
			//A_OverlayOffset(-3, 0 - 18, 32 - 54);
			COD_HandleWeaponFeedback(2, 3, -0.20, frandom(+0.15, -0.15), 0, 0, 0);
			A_ZoomFactor(1.2-0.01);
		}
		else
		{
			//A_OverlayOffset(-3, 0 + 24, 32 - 42);
			COD_HandleWeaponFeedback(2, 3, -0.40, frandom(+0.30, -0.30), -5, 0, 0);
			A_ZoomFactor(1-0.01);
		}
		//gunsmoke
		A_TakeInventory("COD_USP45_Mag", 1);
	}
	
	States
	{
	
	Deselect:
		TNT1 A 0 A_ZoomFactor(1);
		TNT1 A 0 A_StartSound("usp45/lower", 0, CHANF_OVERLAP, 1);
		P99A EDCBA 1;
		PSTG A 0 A_Lower(25);
		Wait;
	User4:
		P99A ED 1;
		TNT1 A 0 A_ZoomFactor(1);
		TNT1 A 0 A_TakeInventory("AimingToken");
		P99A CBA 1;
		Goto NVToggle;
	User2:
		P99A ED 1;
		TNT1 A 0 A_ZoomFactor(1);
		TNT1 A 0 A_TakeInventory("AimingToken");
		P99A CBA 1;
		Goto RadToggle;
	User3:
		TNT1 A 0 A_ZoomFactor(1);
		TNT1 A 0 A_TakeInventory("AimingToken");
		Goto KnifeAttack;
	Select:
		TNT1 A 0
		{
			A_TakeInventory("AimingToken");
			A_TakeInventory("DoAttachment");
		}
		TNT1 A 0 A_Raise(25);
		Wait;
	Ready:
		TNT1 A 0 A_StartSound("usp45/raise", 0, CHANF_OVERLAP, 1);
		P99A ABCDE 1;
	WeaponReady:
		P99A F 1 COD_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER2 | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		TNT1 A 0 A_JumpIf(CountInv("COD_USP45_Mag") == 0, "WeaponReadyEmpty");
		TNT1 A 0 A_JumpIfInventory("ThrowGrenade", 1, "FragGrenade");
		TNT1 A 0 A_JumpIfInventory("ThrowBang", 1, "StunGrenade");
		Loop;
	WeaponReady2:
		P99C D 1 COD_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER2 | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		TNT1 A 0 A_JumpIf(CountInv("COD_USP45_Mag") == 0, "WeaponReady2Empty");
		TNT1 A 0 A_JumpIfInventory("ThrowGrenade", 1, "FragGrenade");
		TNT1 A 0 A_JumpIfInventory("ThrowBang", 1, "StunGrenade");
		Loop;
	WeaponReadyEmpty:
		P99B B 1 COD_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER2 | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		TNT1 A 0 A_JumpIfInventory("ThrowGrenade", 1, "FragGrenade");
		TNT1 A 0 A_JumpIfInventory("ThrowBang", 1, "StunGrenade");
		Loop;
	WeaponReady2Empty:
		P99D B 1 COD_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER2 | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		TNT1 A 0 A_JumpIfInventory("ThrowGrenade", 1, "FragGrenade");
		TNT1 A 0 A_JumpIfInventory("ThrowBang", 1, "StunGrenade");
		Loop;
	Fire:
		TNT1 A 0 A_JumpIfInventory("AimingToken", 1, "Fire2");
		TNT1 A 0 A_JumpIf(CountInv("COD_USP45_Mag") == 0, "DryFire");
		P99B A 1 FireWeapon();
		TNT1 A 0 A_ZoomFactor(1);
		P99B C 1;
		TNT1 A 0 A_JumpIf(CountInv("COD_USP45_Mag") == 0, "WeaponReadyEmpty");
		P99B B 1;
		P99B DE 1 COD_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER2 | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		Goto WeaponReady;
	Fire2:
		TNT1 A 0 A_JumpIf(CountInv("COD_USP45_Mag") == 0, "DryFire2");
		P99D A 1 FireWeapon();
		TNT1 A 0 A_ZoomFactor(1.2);
		P99D B 1;
		TNT1 A 0 A_JumpIf(CountInv("COD_USP45_Mag") == 0, "WeaponReady2Empty");
		P99D C 1;
		P99D DE 1 COD_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER2 | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		Goto WeaponReady2;
	AltFire:
		TNT1 A 0 A_JumpIfInventory("AimingToken", 1, "AltFire2");
		TNT1 A 0 A_GiveInventory("AimingToken");
		TNT1 A 0 A_StartSound("uni/clothfoleys", 0, CHANF_OVERLAP);
		P99C AB 1;
		TNT1 A 0 A_ZoomFactor(1.2);
		P99C CEF 1;
		TNT1 A 0 A_JumpIf(CountInv("COD_USP45_Mag") == 0, "WeaponReady2Empty");
		Goto WeaponReady2;
	AltFire2:
		TNT1 A 0 A_TakeInventory("AimingToken");
		TNT1 A 0 A_StartSound("uni/clothfoleys", 0, CHANF_OVERLAP);
		P99C FE 1;
		TNT1 A 0 A_ZoomFactor(1);
		P99C CBA 1;
		TNT1 A 0 A_JumpIf(CountInv("COD_USP45_Mag") == 0, "WeaponReadyEmpty");
		Goto WeaponReady;
	
	DryFire:
		P99B B 1;
		Goto WeaponReadyEmpty;
	DryFire2:
		P99D A 1;
		Goto WeaponReady2Empty;
	
	UnAimReload:
		TNT1 A 0 A_TakeInventory("AimingToken");
		P99C ED 1;
		TNT1 A 0 A_ZoomFactor(1);
		P99C CBA 1;
	Reload:
		TNT1 A 0 A_JumpIf(CountInv("AimingToken") == 1, "UnAimReload");
		TNT1 A 0 A_JumpIf(CountInv("COD_USP45_Mag") == 0 && CountInv("Clip") == 0, "WeaponReadyEmpty");
		TNT1 A 0 A_JumpIf(CountInv("COD_USP45_Mag") == 13 || CountInv("Clip") == 0, "WeaponReady");
		TNT1 A 0 A_JumpIf(CountInv("COD_USP45_Mag") == 0, "Reload2");
		TNT1 A 0 A_TakeInventory("AimingToken");
		TNT1 A 0 A_StartSound("uni/clothfoleym");
		P99E ABCDEFGHIJ 1;
		TNT1 A 0 A_StartSound("usp45/magout", 0, CHANF_OVERLAP);
		P99E KLMNOP 1;
		TNT1 A 0 A_StartSound("uni/clothfoleys", 0, CHANF_OVERLAP);
		P99E QRSTUVWXYZ 1;
		P99F AB 1;
		TNT1 A 0 A_StartSound("uni/magfol", 0, CHANF_OVERLAP);
		P99F CDEFGHIJK 1;
		TNT1 A 0 A_StartSound("usp45/magin", 0, CHANF_OVERLAP);
		TNT1 A 0 COD_AmmoIntoMag("COD_USP45_Mag", "Clip", 13, 1);
		P99F LM 1;
		P99F N 2;
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP);
		P99F OPQRSTUV 1;
		Goto WeaponReady;
	Reload2:
		TNT1 A 0 A_TakeInventory("AimingToken");
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP);
		P99G ABCDEFG 1;
		P99G H 2;
		TNT1 A 0 A_StartSound("usp45/magout", 0, CHANF_OVERLAP);
		P99G IJKLM 1;
		TNT1 A 0 A_StartSound("uni/magfol", 0, CHANF_OVERLAP);
		P99G NO 1;
		P99G PQRST 2;
		P99G UVWXYZ 1;
		P99H ABC 1;
		TNT1 A 0 A_StartSound("usp45/magin", 0, CHANF_OVERLAP);
		TNT1 A 0 COD_AmmoIntoMag("COD_USP45_Mag", "Clip", 12, 1);
		P99H DE 1;
		P99H F 4;
		P99H GH 1;
		P99H IJKL 1;
		TNT1 A 0 A_StartSound("usp45/lock", 0, CHANF_OVERLAP);
		P99H M 1;
		P99H N 4;
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP);
		P99H OPQR 1;
		Goto WeaponReady;
 	Spawn:
		B001 G -1;
		Stop;
	
	}
}

Class COD_USP45_Mag : Ammo
{
	Default
	{
		Inventory.Amount 0;
		Inventory.MaxAmount 13;
		Ammo.BackpackAmount 0;
		Ammo.BackpackMaxAmount 13;
	}
}
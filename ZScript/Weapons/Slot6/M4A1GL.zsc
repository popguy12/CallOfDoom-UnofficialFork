class COD_M4A1GL : CODWeapon
{
 	Default
	{
		Weapon.AmmoType "Clip";
		Weapon.AmmoUse 0;
		Weapon.AmmoGive 20;
		Weapon.AmmoType2 "COD_M4A1_Mag";
		Weapon.AmmoUse2 0;
		Weapon.AmmoGive2 0;
		
		Weapon.SlotNumber 6;
		
		/*
		zoom 1.0
		zoomaim 1.2
		zoomdelta 0.02
		zoom tics 1
		*/
		
		Obituary "$OB_MPPISTOL";
		Inventory.Pickupmessage "[6] M4A1 Grenadier";
		Tag "M4A1 Grenadier";
		Scale 0.16;
		Inventory.PickupSound "weapons/pistol/pickup";
		+WEAPON.NOALERT;
		+WEAPON.AMMO_OPTIONAL;
		-WEAPON.NOAUTOFIRE;
		
		CODWeapon.SlotType 1;
		CODWeapon.InfoGraphics "ISM4A1", "ICM27N", "ICM27U", "WLM27";
	}
	
	action void FireWeapon()
	{
		A_AlertMonsters();
		A_Overlay(-3, "MuzzleMedium");
		A_OverlayFlags(-3, PSPF_ALPHA, true);
		A_OverlayFlags(-3, PSPF_RENDERSTYLE, true);
		A_OverlayRenderstyle(-3, STYLE_ADD);
		
		A_StartSound("m4gl/fire1", 0, CHANF_OVERLAP, 1);
		A_StartSound("m4gl/fire2", 0, CHANF_OVERLAP, 0.75);
		COD_FireBullets("COD_556x45mm", 1, frandom(-0.1, 0.1), 0, 0, frandom(-0.1, 0.1));
		A_SpawnItemEx("PlayerMuzzleFlash",30,0,45);
		
		//this handles placing the flash correctly
		if(CountInv("AimingToken"))
		{
			A_OverlayOffset(-3, 0 - 24, 32 - 40);
			COD_HandleWeaponFeedback(2, 2, -0.80, frandom(+0.60, -0.60), 0, 0, 0);
			A_ZoomFactor(1.2-0.02);
		}
		else
		{
			A_OverlayOffset(-3, 0 + 10, 32 - 42);
			COD_HandleWeaponFeedback(2, 2, -1.60, frandom(+1.20, -1.20), -4, 0, 0);
			A_ZoomFactor(1-0.02);
		}
		//gunsmoke
		A_TakeInventory("COD_M4A1_Mag", 1);
	}
	
	States
	{
	
	Deselect:
		TNT1 A 0 A_StartSound("m4gl/lower", 0, CHANF_OVERLAP, 1);
		TNT1 A 0 A_ZoomFactor(1);
		M27A FJIHGEDCBA 1;
		PSTG A 0 A_Lower(25);
		Wait;
	User4:
		M27A FJIHGED 1;
		TNT1 A 0 A_ZoomFactor(1);
		TNT1 A 0 A_TakeInventory("AimingToken");
		M27A CBA 1;
		Goto NVToggle;
	User2:
		M27A FJIHGED 1;
		TNT1 A 0 A_ZoomFactor(1);
		TNT1 A 0 A_TakeInventory("AimingToken");
		M27A CBA 1;
		Goto RadToggle;
	User3:
		TNT1 A 0 A_ZoomFactor(1);
		TNT1 A 0 A_TakeInventory("AimingToken");
		Goto KnifeAttack;
	Select:
		TNT1 A 0
		{
			A_TakeInventory("AimingToken");
		}
		TNT1 A 0 A_Raise(25);
		Wait;
	Ready:
		TNT1 A 0 A_StartSound("M4GL/raise", 0, CHANF_OVERLAP, 1);
		TNT1 A 10;
		M27H EFGHIJLKMNOP 0;
		M27A ABCDEGHIJF 1;
	WeaponReady:
		M27A F 1 COD_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER2 | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		TNT1 A 0 A_JumpIfInventory("ThrowGrenade", 1, "FragGrenade");
		TNT1 A 0 A_JumpIfInventory("ThrowBang", 1, "StunGrenade");
		Loop;
	WeaponReady2:
		M27C D 1 COD_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER2 | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		TNT1 A 0 A_JumpIfInventory("ThrowGrenade", 1, "FragGrenade");
		TNT1 A 0 A_JumpIfInventory("ThrowBang", 1, "StunGrenade");
		Loop;
	Fire:
		TNT1 A 0 A_JumpIfInventory("AimingToken", 1, "Fire2");
		TNT1 A 0 A_JumpIf(CountInv("COD_M4A1_Mag") == 0, "DryFire");
		M27B A 1 FireWeapon();
		TNT1 A 0 A_ZoomFactor(1);
		M27B BC 1;
		M27B DE 1 A_ReFire("Fire");
		Goto WeaponReady;
	Fire2:
		TNT1 A 0 A_JumpIf(CountInv("COD_M4A1_Mag") == 0, "DryFire2");
		M27D A 1 FireWeapon();
		TNT1 A 0 A_ZoomFactor(1.2);
		M27D BC 1;
		M27D DE 1 A_ReFire("Fire2");
		Goto WeaponReady2;
	AltFire:
		TNT1 A 0 A_JumpIfInventory("AimingToken", 1, "AltFire2");
		TNT1 A 0 A_GiveInventory("AimingToken");
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP);
		M27C ABC 1;
		TNT1 A 0 A_ZoomFactor(1.2);
		M27C EFGD 1;
		Goto WeaponReady2;
	AltFire2:
		TNT1 A 0 A_TakeInventory("AimingToken");
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP);
		M27C DGFE 1;
		TNT1 A 0 A_ZoomFactor(1);
		M27C CBA 1;
		Goto WeaponReady;
	
	DryFire:
		M27B E 1;
		Goto WeaponReady;
	DryFire2:
		M27D E 1;
		Goto WeaponReady2;
	
	UnAimReload:
		TNT1 A 0 A_TakeInventory("AimingToken");
		M3GC DGFE 1;
		TNT1 A 0 A_ZoomFactor(1);
		M3GC CBA 1;
	Reload:
		TNT1 A 0 A_JumpIf(CountInv("AimingToken") == 1, "UnAimReload");
		TNT1 A 0 A_JumpIf(CountInv("COD_M4A1_Mag") == 0 && CountInv("Clip") == 0, "WeaponReadyEmpty");
		TNT1 A 0 A_JumpIf(CountInv("COD_M4A1_Mag") == 61 || CountInv("Clip") == 0, "WeaponReady");
		TNT1 A 0 A_JumpIf(CountInv("COD_M4A1_Mag") == 0, "ReloadEmpty");
		TNT1 A 0 A_TakeInventory("AimingToken");
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP);
		M27E ABCDEFGH 1;
		TNT1 A 0 A_StartSound("M4GL/MagOut", 0, CHANF_OVERLAP);
		M27E IJKLMNOP 1;
		TNT1 A 0 A_StartSound("uni/clothfoleys", 0, CHANF_OVERLAP);
		M27E QRSTUV 1;
		TNT1 A 0 A_StartSound("uni/magfol", 0, CHANF_OVERLAP);
		M27E WXZ 2;
		M27F ABCD 2;
		M27F EFG 1;
		TNT1 A 0 A_StartSound("m4gl/magin", 0, CHANF_OVERLAP);
		TNT1 A 0 COD_AmmoIntoMag("COD_M4A1_Mag", "Clip", 61, 1);
		M27F HIJKL 1;
		M27F MN 2;
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP);
		M27F OPQRST 1;
		Goto WeaponReady;
	ReloadEmpty:
		TNT1 A 0 A_TakeInventory("AimingToken");
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP);
		M27G ABCD 1;
		TNT1 A 0 A_StartSound("uni/clothfoleys", 0, CHANF_OVERLAP);
		M27G EF 1;
		TNT1 A 0 A_StartSound("m4gl/magdrop", 0, CHANF_OVERLAP);
		M27G GHIJ 1;
		TNT1 A 0 A_StartSound("uni/clothfoleys", 0, CHANF_OVERLAP);
		M27G KLM 2;
		M27G NOP 1;
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP);
		M27G QRST 1;
		TNT1 A 0 A_StartSound("uni/magfol", 0, CHANF_OVERLAP);
		M27G UVW 1;
		TNT1 A 0 A_StartSound("m4gl/magin", 0, CHANF_OVERLAP);
		M27G XYZ 1;
		M27H A 1;
		TNT1 A 0 COD_AmmoIntoMag("COD_M4A1_Mag", "Clip", 60 , 1);
		M27H BCD 2;
		TNT1 A 0 A_StartSound("uni/clothfoleys", 0, CHANF_OVERLAP);
		M27H EFG 1;
		TNT1 A 0 A_StartSound("m4gl/bolt", 0, CHANF_OVERLAP);
		M27H HIJ 2;
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP);
		M27F PQRST 1;
		//N27H KLMNOP 1; [Pop] SOME FUCKING REASON THESE ARENT RENDERING?
		//IS THE SOURCEPORT STUPID? 
		Goto WeaponReady;
 	Spawn:
		B002 O -1;
		Stop;
	
	}
}

Class COD_M4A1_Mag : Ammo
{
	Default
	{
		Inventory.Amount 0;
		Inventory.MaxAmount 61;
		Ammo.BackpackAmount 0;
		Ammo.BackpackMaxAmount 61;
	}
}
class COD_SKS : CODWeapon
{
 	Default
	{
		Weapon.AmmoType "Clip";
		Weapon.AmmoUse 0;
		Weapon.AmmoGive 5;
		Weapon.AmmoType2 "COD_SKS_Mag";
		Weapon.AmmoUse2 0;
		Weapon.AmmoGive2 0;
		
		Weapon.SlotNumber 4;
		
		/*
		zoom 1.0
		zoomaim 1.2
		zoomdelta 0.2
		zoom tics 1
		*/
		
		Obituary "$OB_MPPISTOL";
		Inventory.Pickupmessage "[4] SKS";
		Tag "SKS";
		Scale 0.16;
		Inventory.PickupSound "weapons/pistol/pickup";
		+WEAPON.NOALERT;
		+WEAPON.AMMO_OPTIONAL;
		
		CODWeapon.SlotType 1;
		CODWeapon.InfoGraphics "ISSKS", "ICSKSN", "ICSKSU", "WLSKS";
	}
	
	action void FireWeapon()
	{
		A_AlertMonsters();
		A_Overlay(-3, "MuzzleMedium");
		A_OverlayFlags(-3, PSPF_ALPHA, true);
		A_OverlayFlags(-3, PSPF_RENDERSTYLE, true);
		A_OverlayRenderstyle(-3, STYLE_ADD);
		
		A_StartSound("sks/fire", 0, CHANF_OVERLAP, 1);
		COD_FireBullets("COD_762x39mm", 1, frandom(-0.1, 0.1), 0, 0, frandom(-0.1, 0.1));
		A_SpawnItemEx("PlayerMuzzleFlash",30,0,45);
		
		//this handles placing the flash correctly
		if(CountInv("AimingToken"))
		{
			A_OverlayOffset(-3, 0 - 18, 32 - 54);
			COD_HandleWeaponFeedback(3, 3, -0.60, frandom(+0.40, -0.40), 0, 0, 0);
			A_ZoomFactor(1.2-0.2);
		}
		else
		{
			A_OverlayOffset(-3, 0 + 12, 32 - 50);
			COD_HandleWeaponFeedback(3, 3, -1.2, frandom(+0.8, -0.8), -3, 0, 0);
			A_ZoomFactor(1-0.2);
		}
		//gunsmoke
		A_TakeInventory("COD_SKS_Mag", 1);
	}
	
	States
	{
	Deselect:
		TNT1 A 0 A_StartSound("SKS/lower", 0, CHANF_OVERLAP, 1);
		TNT1 A 0 A_ZoomFactor(1);
		SVTA FHGEDCBA 1;
		PSTG A 0 A_Lower(25);
		Wait;
	User4:
		SVTA FHGED 1;
		TNT1 A 0 A_ZoomFactor(1);
		TNT1 A 0 A_TakeInventory("AimingToken");
		SVTA CBA 1;
		Goto NVToggle;
	User2:
		SVTA FHGED 1;
		TNT1 A 0 A_ZoomFactor(1);
		TNT1 A 0 A_TakeInventory("AimingToken");
		SVTA CBA 1;
		Goto RadToggle;
	User3:
		TNT1 A 0 A_JumpIfInventory("AimingToken", 1, "WeaponReady2");
		TNT1 A 0 A_GiveInventory("AimingToken", 1);
		SVTI ABC 1 A_Recoil(2);
		TNT1 A 0 A_StartSound("melee/knife/slash", 0, CHANF_OVERLAP, 1);
		SVTI DEF 1
		{
			A_Recoil(-5);
			A_CustomPunch(24, 1, CPF_PULLIN | CPF_NOTURN, "BulletPuff", 96, 0, 0, "BasicArmorBonus", "melee/bayonet/stab");
			//A_StartSound("melee/bayonet/stab", 0, CHANF_OVERLAP, 1);
		}
		SVTI GHIJ 2;
		TNT1 A 0 A_TakeInventory("AimingToken", 1);
		Goto WeaponReady;
	Select:
		TNT1 A 0
		{
			A_TakeInventory("AimingToken");
		}
		TNT1 A 0 A_Raise(25);
		Wait;
	Ready:
		TNT1 A 0 A_StartSound("SKS/raise", 0, CHANF_OVERLAP, 1);
		TNT1 A 12;
		SVTA ABCDEGHF 1;
	WeaponReady:
		SVTA F 1 COD_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER2 | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		TNT1 A 0 A_JumpIf(CountInv("COD_SKS_Mag") == 0, "WeaponReadyEmpty");
		TNT1 A 0 A_JumpIfInventory("ThrowGrenade", 1, "FragGrenade");
		TNT1 A 0 A_JumpIfInventory("ThrowBang", 1, "StunGrenade");
		Loop;
	WeaponReady2:
		SVTC D 1 COD_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER2 | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		TNT1 A 0 A_JumpIf(CountInv("COD_SKS_Mag") == 0, "WeaponReady2Empty");
		TNT1 A 0 A_JumpIfInventory("ThrowGrenade", 1, "FragGrenade");
		TNT1 A 0 A_JumpIfInventory("ThrowBang", 1, "StunGrenade");
		Loop;
	WeaponReadyEmpty:
		SVTB A 1 COD_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER2 | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		TNT1 A 0 A_JumpIfInventory("ThrowGrenade", 1, "FragGrenade");
		TNT1 A 0 A_JumpIfInventory("ThrowBang", 1, "StunGrenade");
		Loop;
	WeaponReady2Empty:
		SVTD A 1 COD_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER2 | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		TNT1 A 0 A_JumpIfInventory("ThrowGrenade", 1, "FragGrenade");
		TNT1 A 0 A_JumpIfInventory("ThrowBang", 1, "StunGrenade");
		Loop;
	Fire:
		TNT1 A 0 A_JumpIfInventory("AimingToken", 1, "Fire2");
		TNT1 A 0 A_JumpIf(CountInv("COD_SKS_Mag") == 0, "DryFire");
		SVTA F 1 FireWeapon();
		TNT1 A 0 A_ZoomFactor(1);
		SVTB BA 1;
		TNT1 A 0 A_JumpIf(CountInv("COD_SKS_Mag") == 0, "WeaponReadyEmpty");
		SVTB BC 1;
		SVTB DEFGHI 1 COD_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER2 | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		Goto WeaponReady;
	Fire2:
		TNT1 A 0 A_JumpIf(CountInv("COD_SKS_Mag") == 0, "DryFire2");
		SVTC D 1 FireWeapon();
		TNT1 A 0 A_ZoomFactor(1.2);
		SVTD BA 1;
		TNT1 A 0 A_JumpIf(CountInv("COD_SKS_Mag") == 0, "WeaponReady2Empty");
		SVTD BC 1;
		SVTD DEFGHI 1 COD_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER2 | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		Goto WeaponReady2;
	AltFire:
		TNT1 A 0 A_JumpIfInventory("AimingToken", 1, "AltFire2");
		TNT1 A 0 A_GiveInventory("AimingToken");
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP, 1);
		SVTC ABC 1;
		TNT1 A 0 A_ZoomFactor(1.2);
		SVTC EFD 1;
		Goto WeaponReady2;
	AltFire2:
		TNT1 A 0 A_TakeInventory("AimingToken");
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP, 1);
		SVTC DFE 1;
		TNT1 A 0 A_ZoomFactor(1);
		SVTC CBA 1;
		Goto WeaponReady;
	
	DryFire:
		SVTB A 1;
		Goto WeaponReadyEmpty;
	DryFire2:
		SVTD A 1;
		Goto WeaponReady2Empty;
	
	UnAimReload:
		TNT1 A 0 A_TakeInventory("AimingToken");
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP, 1);
		SVTC FDE 1;
		TNT1 A 0 A_ZoomFactor(1);
		SVTC CBA 1;
	Reload:
		TNT1 A 0 A_JumpIf(CountInv("AimingToken") == 1, "UnAimReload");
		TNT1 A 0 A_JumpIf(CountInv("COD_SKS_Mag") == 0 && CountInv("Clip") == 0, "WeaponReadyEmpty");
		TNT1 A 0 A_JumpIf(CountInv("COD_SKS_Mag") == 10 || CountInv("Clip") == 0, "WeaponReady");
		TNT1 A 0 A_JumpIf(CountInv("COD_SKS_Mag") == 0, "ReloadEmpty");
		TNT1 A 0 A_JumpIf(CountInv("COD_SKS_Mag") == 1, "ReloadEjectLast");
		TNT1 A 0 A_TakeInventory("AimingToken");
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP, 1);
		SVTE ABCDEFG 1;
		TNT1 A 0 A_StartSound("sks/magopen", 0, CHANF_OVERLAP, 1);
		SVTE HIJKLMNOP 1;
		TNT1 A 0 A_StartSound("sks/magclose", 0, CHANF_OVERLAP, 1);
		SVTE QRSTUVWX 1;
		TNT1 A 0 A_StartSound("uni/clothfoleys", 0, CHANF_OVERLAP, 1);
		SVTE YZ 1;
		TNT1 A 0 A_StartSound("SKS/rackback", 0, CHANF_OVERLAP, 1);
		SVTF ABCDE 2;
		SVTF FGHIJK 1;
		TNT1 A 0 A_StartSound("uni/cartfoley", 0, CHANF_OVERLAP, 1);
		SVTF LM 1;
	ReloadInsert:
		SVTF NOP 1;
		TNT1 A 0 A_StartSound("sks/clipset", 0, CHANF_OVERLAP, 1);
		SVTF QRST 2;
		TNT1 A 0 A_StartSound("uni/clothfoleys", 0, CHANF_OVERLAP, 1);
		SVTF UVW 2;
		TNT1 A 0 A_StartSound("sks/clipin", 0, CHANF_OVERLAP, 1);
		SVTF XY 1;
		SVTF Z 2;
		SVTG ABCD 2;
		TNT1 A 0 COD_AmmoIntoMag("COD_SKS_Mag", "Clip", 10, 1);
		TNT1 A 0 A_StartSound("uni/clothfoleys", 0, CHANF_OVERLAP, 1);
		SVTG EFGHIJKLM 1;
		TNT1 A 0 A_StartSound("sks/cliprem", 0, CHANF_OVERLAP, 1);
		SVTG NOPQR 2;
		TNT1 A 0 A_StartSound("uni/clothfoleys", 0, CHANF_OVERLAP, 1);
		SVTG STUV 1;
		TNT1 A 0 A_StartSound("sks/rackforward", 0, CHANF_OVERLAP, 1);
		SVTG WXYZ 2;
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP, 1);
		SVTH ABCDEF 1;
		Goto WeaponReady;
	ReloadEmpty:
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP, 1);
		SVTJ ABCDEF 1;
		Goto ReloadInsert;
	ReloadEjectLast:
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP, 1);
		SVTE ABCDEFG 1;
		SVTE UVWXYZ 1;
		TNT1 A 0 A_StartSound("sks/rackback", 0, CHANF_OVERLAP, 1);
		SVTF ABCDE 2;
		SVTF FGHIJKLM 1;
		Goto ReloadInsert;
 	Spawn:
		B003 A -1;
		Stop;
	
	}
}

Class COD_SKS_Mag : Ammo
{
	Default
	{
		Inventory.Amount 0;
		Inventory.MaxAmount 10;
		Ammo.BackpackAmount 0;
		Ammo.BackpackMaxAmount 10;
	}
}
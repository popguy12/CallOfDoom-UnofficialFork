class COD_M2Flamethrower : CODWeapon
{
 	Default
	{	
		Weapon.AmmoType "Cell";
		Weapon.AmmoUse 0;
		Weapon.AmmoGive 4;
		
		Weapon.SlotNumber 9;
		Weapon.SlotPriority 3;
		
		/*
		zoom 1.0
		zoomaim 1.2
		zoomdelta 0.01
		zoom tics 1
		*/
		
		Obituary "$OB_MPPISTOL";
		Inventory.Pickupmessage "[9] M2 Flamethrower";
		Tag "M2 Flamethrower";
		Scale 0.16;
		Inventory.PickupSound "weapons/pistol/pickup";
		+WEAPON.NOALERT;
		+WEAPON.AMMO_OPTIONAL;
		+WEAPON.NOAUTOFIRE;
		
		CODWeapon.PlaySpeed 0.8;
		CODWeapon.SlotType 1;
		CODWeapon.InfoGraphics "ISM2F", "ICM2FN", "ICM2FU", "WLM2F";
	}
	
	action void FireWeapon(bool take = true)
	{
		A_Fireprojectile("COD_FlameProjectile",0,0);
		
		A_SpawnItemEx("PlayerMuzzleFlash",30,0,45);
		
		COD_HandleWeaponFeedback(2, 2, -0.20, frandom(+0.15, -0.15), -4, 0, 0);
		A_ZoomFactor(1-0.02);
		if(take)
		{
			A_TakeInventory("Cell", 1);
		}
	}
	
	States
	{
	
	Deselect:
		TNT1 A 0 A_StartSound("M2Flame/Lower", 0, CHANF_OVERLAP);
		M2FA LKJIHGFEDCBA 1;
		PSTG A 0 A_Lower(25);
		Wait;
	User4:
		TNT1 A 0 A_ZoomFactor(1);
		M2FA LKJIHGFEDCBA 1;
		Goto NVToggle;
	User2:
		TNT1 A 0 A_ZoomFactor(1);
		M2FA LKJIHGFEDCBA 1;
		Goto RadToggle;
	User3:
		TNT1 A 0 A_ZoomFactor(1);
		Goto KnifeAttack;
	Select:
		TNT1 A 0
		{
			A_TakeInventory("AimingToken");
		}
		TNT1 A 0 A_Raise(25);
		Wait;
	Ready:
		TNT1 A 16;
		TNT1 A 0 A_StartSound("M2Flame/Raise", 0, CHANF_OVERLAP);
		M2FA ABCDEFGHIJKL 1;
	WeaponReady:
		M2FB A 1 COD_WeaponReady(WRF_ALLOWUSER2 | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		TNT1 A 0 A_JumpIfInventory("ThrowGrenade", 1, "FragGrenade");
		TNT1 A 0 A_JumpIfInventory("ThrowBang", 1, "StunGrenade");
		Loop;
	Fire:
		TNT1 A 0 A_JumpIf(CountInv("Cell") == 0, "DryFire");
		M2FB A 4 A_StartSound("M2Flame/FireStart", 0, CHANF_OVERLAP);
	FireLoop:
		TNT1 A 0 A_StartSound("M2Flame/FireLoop", 20, CHANF_NOSTOP);
		M2FB A 1 FireWeapon();
		M2FB B 1 A_ZoomFactor(1);
		M2FB C 1 FireWeapon(false);
		M2FB D 1 A_ZoomFactor(1);
		TNT1 A 0 A_ReFire("FireLoop");
	FireStop:
		TNT1 A 0 A_StopSound(20);
		M2FB A 4 A_StartSound("M2Flame/FireStop", 0, CHANF_OVERLAP);
		Goto WeaponReady;
	
	AltFire:
		M2FB A 1;
		Goto WeaponReady;
	
	DryFire:
		M2FB A 1;
		Goto WeaponReady;
	
 	Spawn:
		B004 F -1; //K frame for empty
		Stop;
	
	}
}
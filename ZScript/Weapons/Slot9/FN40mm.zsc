class COD_FN40 : CODWeapon
{
 	Default
	{	
		Weapon.AmmoType "RocketAmmo";
		Weapon.AmmoUse 0;
		Weapon.AmmoGive 4;
		Weapon.AmmoType2 "COD_FN40Tube";
		Weapon.AmmoUse2 0;
		Weapon.AmmoGive2 0;
		
		Weapon.SlotNumber 9;
		Weapon.SlotPriority 3;
		
		/*
		zoom 1.0
		zoomaim 1.2
		zoomdelta 0.01
		zoom tics 1
		*/
		
		Obituary "$OB_MPPISTOL";
		Inventory.Pickupmessage "[9] FN 40mm Launcher";
		Tag "FN 40mm Launcher";
		Scale 0.16;
		Inventory.PickupSound "weapons/pistol/pickup";
		+WEAPON.NOALERT;
		+WEAPON.AMMO_OPTIONAL;
		+WEAPON.NOAUTOFIRE;
		
		CODWeapon.PlaySpeed 0.8;
		CODWeapon.SlotType 3;
		CODWeapon.InfoGraphics "ISFNGL40", "ICM320N", "ICM320U", "WLM320";
	}
	
	action void FireWeapon()
	{
		A_StartSound("m4gl/gl/fire", 0, CHANF_OVERLAP, 1);
		//COD_FireBullets("COD_556x45mm", 1, frandom(-0.1, 0.1), 0, 0, frandom(-0.1, 0.1));
		A_FireProjectile("COD_40mmGrenade");
		
		A_SpawnItemEx("PlayerMuzzleFlash",30,0,45);
		
		//this handles placing the flash correctly
		if(CountInv("AimingToken"))
		{
			COD_HandleWeaponFeedback(2, 2, -0.40, frandom(+0.20, -0.20), 0, 0, 0);
			A_ZoomFactor(1.2-0.02);
		}
		else
		{
			COD_HandleWeaponFeedback(2, 2, -0.60, frandom(+0.40, -0.40), -4, 0, 0);
			A_ZoomFactor(1-0.02);
		}
		//gunsmoke
		A_TakeInventory("COD_FN40Tube", 1);
	}
	
	States
	{
	
	Deselect:
		TNT1 A 0 A_StartSound("FN40/Lower", 0, CHANF_OVERLAP);
		GLMA FIHGEDCBA 1;
		PSTG A 0 A_Lower(25);
		Wait;
	User4:
		TNT1 A 0 A_ZoomFactor(1);
		U45E GFEDCBA 1;
		Goto NVToggle;
	User2:
		TNT1 A 0 A_ZoomFactor(1);
		U45E GFEDCBA 1;
		Goto RadToggle;
	User3:
		TNT1 A 0 A_ZoomFactor(1);
		Goto KnifeAttack;
	Select:
		TNT1 A 0
		{
			A_TakeInventory("AimingToken");
		}
		TNT1 A 0 A_Raise(25);
		Wait;
	Ready:
		TNT1 A 5;
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP);
		TNT1 A 0 A_StartSound("FN40/Raise1", 0, CHANF_OVERLAP);
		GLMA ABCDE 1;
		TNT1 A 0 A_StartSound("FN40/Raise2", 0, CHANF_OVERLAP);
		GLMA GHIF 1;
	WeaponReady:
		GLMA F 1 COD_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER2 | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		TNT1 A 0 A_JumpIfInventory("ThrowGrenade", 1, "FragGrenade");
		TNT1 A 0 A_JumpIfInventory("ThrowBang", 1, "StunGrenade");
		Loop;
	WeaponReady2:
		GLMC D 1 COD_WeaponReady(WRF_ALLOWRELOAD | WRF_ALLOWUSER2 | WRF_ALLOWUSER3 | WRF_ALLOWUSER4);
		TNT1 A 0 A_JumpIfInventory("ThrowGrenade", 1, "FragGrenade");
		TNT1 A 0 A_JumpIfInventory("ThrowBang", 1, "StunGrenade");
		Loop;
	Fire:
		TNT1 A 0 A_JumpIfInventory("AimingToken", 1, "Fire2");
		TNT1 A 0 A_JumpIf(CountInv("COD_FN40Tube") == 0, "DryFire");
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP);
		GLMB A 1 FireWeapon();
		TNT1 A 0 A_ZoomFactor(1);
		GLMB BCDE 1;
		Goto WeaponReady;
	Fire2:
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP);
		TNT1 A 0 A_JumpIf(CountInv("COD_FN40Tube") == 0, "DryFire2");
		GLMD A 1 FireWeapon();
		TNT1 A 0 A_ZoomFactor(1.2);
		GLMD BCDE 1;
		Goto WeaponReady2;
	AltFire:
		TNT1 A 0;
		TNT1 A 0 A_JumpIfInventory("AimingToken", 1, "AltFire2");
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP);
		GLMC ABC 1;
		TNT1 A 0 A_ZoomFactor(1.2);
		GLMC EFGHID 1;
		TNT1 A 0 A_GiveInventory("AimingToken", 1);
		Goto WeaponReady2;
	AltFire2:
		TNT1 A 0;
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP);
		GLMC DIHGFE 1;
		TNT1 A 0 A_ZoomFactor(1);
		GLMC CBA 1;
		TNT1 A 0 A_TakeInventory("AimingToken", 1);
		Goto WeaponReady;
	
	DryFire:
		GLMB E 1;
		Goto WeaponReady;
	DryFire2:
		GLMD E 1;
		Goto WeaponReady2;
	
	UnAimReload:
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP);
		GLMC DIHGFE 1;
		TNT1 A 0 A_ZoomFactor(1);
		GLMC CBA 1;
		TNT1 A 0 A_TakeInventory("AimingToken", 1);
	Reload:
		TNT1 A 0 A_JumpIfInventory("AimingToken", 1, "UnAimReload");
		TNT1 A 0 A_JumpIf(CountInv("COD_FN40Tube") == 1, "WeaponReady");
		TNT1 A 0 A_StartSound("uni/clothfoleys", 0, CHANF_OVERLAP);
		GLME ABCD 1;
		GLME EFGH 2;
		TNT1 A 0 A_StartSound("FN40/Open", 0, CHANF_OVERLAP);
		GLME IJ 1;
		TNT1 A 0 A_StartSound("m4gl/gl/shellout", 0, CHANF_OVERLAP);
		GLME KL 2;
		TNT1 A 0 A_StartSound("uni/clothfoleym", 0, CHANF_OVERLAP);
		GLME MNOPQ 1;
		TNT1 A 0 A_StartSound("uni/cartfoley", 0, CHANF_OVERLAP);
		GLME RSTU 1;
		GLME VWX 1;
		TNT1 A 0 A_StartSound("m4gl/gl/shellin", 0, CHANF_OVERLAP);
		TNT1 A 0 COD_AmmoIntoMag("COD_FN40Tube", "RocketAmmo", 1, 1);
		GLME YZ 2;
		TNT1 A 0 A_StartSound("uni/clothfoleys", 0, CHANF_OVERLAP);
		GLMF A 2;
		GLMF BC 1;
		GLMF D 4;
		GLMF EF 2;
		TNT1 A 0 A_StartSound("FN40/close", 0, CHANF_OVERLAP);
		GLMF GH 2;
		GLMF IJKLMN 1;
		Goto WeaponReady;
	
 	Spawn:
		B001 Z -1; //K frame for empty
		Stop;
	
	}
}

class COD_FN40Tube : Ammo
{
	Default
	{
		Inventory.MaxAmount 1;
	}
}